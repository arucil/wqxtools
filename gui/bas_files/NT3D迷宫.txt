10 GRAPH:BEEP:LOCATE 3,2:PRINT "All rights reserved","   by NOTHEOREM R"
15 CIRCLE 132,54,5:TIME=8:GOSUB 90:CLS:K=0:WHILE PEEK(199)>128:K=ASC(INKEY$):WEND
20 OPEN "nt.sys" FOR RANDOM AS#1::IF LOF(1)=0 THEN 30 else close #1
22 OPEN "map" FOR RANDOM AS#1::IF LOF(1)=0 THEN 30 ELSE 40
30 close #1:BEEP:LOCATE 3,1:PRINT "File missing!       Run MapEditor first!"
35 LINE 0,27,159,27:LINE 0,63,159,63:LINE 3,32,157,32:K=ASC(INKEY$):GOTO 460
40 close #1:OPEN "nt.sys" FOR INPUT AS#1:WHILE M<>555:INPUT #1,M:WEND
45 FOR Y=33 TO 46
50 INPUT #1,M:IF M=0 THEN 60
55 INPUT #1,N:LINE M,Y,N,Y:GOTO 50
60 INPUT #1,M:IF M=8 THEN 70
65 DRAW M,Y:GOTO 60
70 K=0:WHILE PEEK(199)>128:K=ASC(INKEY$):WEND:IF K<>0 THEN 130
80 NEXT Y:GOTO 100
90 FOR I=0 TO 500*TIME:NEXT I:RETURN
100 GOSUB 90:FOR I=0 TO 46:LINE 14,79-I,156,46-I,0:NEXT I:TIME=4:GOSUB 90
130 CLOSE #1:CLS:DIM Q(9):S$="NT-3Dmaze!"
131 FOR I=0 TO 9:P(I)=-I+1:NEXT I
132 FOR M=0 TO 12:FOR N=0 TO 9
133 IF P(N)>3 THEN 138
134 IF P(N)>0 THEN LOCATE P(N),N+6:PRINT " "
135 P(N)=P(N)+1
136 IF P(N)>0 THEN LOCATE P(N),N+6:PRINT MID$(S$,N+1,1)
137 IF P(N)=4 THEN PLAY "L48G"
138 NEXT N:GOSUB 160:NEXT M:GOSUB 90
140 PLAY "P5L10BP20L10CP20L10AP20L10DP20L10GP20L10D "
141 FOR I=0 TO 3:CLS:LOCATE 4,6:PRINT S$:G=15*I:H=10*I:J=2*I:K=13*I
143 CIRCLE 23+G,7+H,16-J:CIRCLE 125-H,55-H,12-J:CIRCLE 108-H,7+H,14-J
144 CIRCLE 127-K,91-G,6-I:CIRCLE 19+K,81-K,8-I:CIRCLE 132-H,10+H,15-J
145 FOR M=0 TO 50:NEXT M:GOSUB 160:NEXT I
150 BOX 50,15,110,50,1,0:LOCATE 3,7:PRINT "『迷宫』":GOSUB 90:TIME=2
155 GOSUB 160:GOSUB 90:LOCATE 4,5:PRINT "             "
156 GOSUB 160:GOSUB 90:LOCATE 4,5:PRINT "press any key":GOTO 155
160 K=0:WHILE PEEK(199)>128:K=ASC(INKEY$):IF K<>0 THEN 180:WEND:RETURN
180 CLEAR:BALL=1:DIM M(22,17)
200 N=2.5:FOR I=5 TO 1 STEP -1:M=I*N:X=6*M:G=1.25*M:H=1.75*M:J=0.5*M:K=1.125*M
220 CLS:CIRCLE 32+X,25-G,13-J:CIRCLE 80-X,39-H,22-K:CIRCLE 80-X,39-H,21-K,1,0
230 CIRCLE 80+X,15+G,9+J:CIRCLE 128-X,25+H,13+K:CIRCLE 128-X,25+H,12+K,1,0
240 N=N-0.4:NEXT I
250 CLS:CIRCLE 32,25,13,1:CIRCLE 31,21,14,1,0:CIRCLE 32,25,13:CIRCLE 128,25,13,1
252 CIRCLE 127,21,14,1,0:CIRCLE 128,25,13:LINE 28,46,43,46:LINE 30,47,39,47
254 LINE 124,46,139,46:LINE 126,47,135,47:CIRCLE 80,15,9:CIRCLE 80,39,22,1
256 CIRCLE 79,34,23,1,0:CIRCLE 80,39,22:LINE 74,73,95,73:LINE 70,74,100,74:LINE 74,75,95,75
260 ON BALL GOTO 261,262,263,264
261 S$="PLAY":GOTO 270
262 S$="QUIT":GOTO 270
263 S$="INFO":GOTO 270
264 S$="VIEW"
270 LOCATE 3,9:PRINT S$
280 K=ASC(INKEY$)
281 IF K<>23 THEN 285
282 IF BALL=1 THEN BALL=4 ELSE BALL=BALL-1
284 GOSUB 300:GOSUB 350:GOTO 250
285 IF K<>22 THEN 290
287 IF BALL=4 THEN BALL=1 ELSE BALL=BALL+1
289 GOSUB 350:GOSUB 300:GOTO 250
290 IF K=27 THEN BALL=2:GOTO 400
292 IF K=13 THEN 400 ELSE BEEP:GOTO 280
300 CLS:LINE 54,67,74,67:LINE 52,68,79,68:LINE 54,69,74,69:CIRCLE 60,34,20
310 CIRCLE 45,23,14:CIRCLE 60,34,19,1,0:LINE 107,54,127,54:LINE 109,55,123,55
320 CIRCLE 115,29,17:CIRCLE 100,20,13:CIRCLE 115,29,16,1,0:RETURN
350 CLS:LINE 94,67,114,67:LINE 92,68,119,68:LINE 94,69,114,69:CIRCLE 100,34,20
360 CIRCLE 115,23,14:CIRCLE 100,34,19,1,0:LINE 37,54,57,54:LINE 39,55,53,55
380 CIRCLE 45,29,17:CIRCLE 60,20,13:CIRCLE 45,29,16,1,0:RETURN
400 N=0.7:FOR I=1 TO 5:M=I*N:X=6*M:G=1.25*M:H=1.75*M:J=0.5*M:K=1.125*M
420 CLS:CIRCLE 128-X,25-G,13-J:CIRCLE 80+X,39-H,22-K:CIRCLE 80+X,39-H,21-K,1,0
430 CIRCLE 80-X,15+G,9+J:CIRCLE 32+X,25+H,13+K:CIRCLE 32+X,25+H,12+K,1,0:N=N+0.4:NEXT I
450 CLS:ON BALL GOTO 600,460,480,470
460 CLS:LOCATE 3,4:PRINT "NT.3D SHUT DOWN":BEEP:END
470 IF READED=0 THEN GOSUB 550
475 X=-1:Y=1:GOSUB 500:GOSUB 540:GOTO 200
480 LOCATE 3,6:PRINT "NT-3Dmaze","    version 1.0":K=ASC(INKEY$):CLS
482 PRINT "感谢下载使用!","NT软件带给您更新更优的技术支持!":K=ASC(INKEY$):CLS
487 PRINT "NTtip!","游戏需读取系统文件和地图数据包.","运行MapEditor会自动为您生成.";
488 K=ASC(INKEY$):CLS:PRINT "[系统按键]",CHR$(30);"前进 ";CHR$(31);"后退 ";CHR$(17);"左移 "
490 PRINT CHR$(16);"右移 ";CHR$(28);"左转 ";CHR$(29);"右转 ","[输入]OR[M]地图","[Q]跳出";
491 K=ASC(INKEY$):CLS
492 PRINT " c NOTHEOREM 2003","EM:notheorem@163.com","QQ:27675496","   燕山大学 赵瑞"
495 CIRCLE 12,8,5:K=ASC(INKEY$):GOTO 200
500 CLS:PM=PEEK(1016):PS=PEEK(1017):BOX 1,0,159,79,1,2:BOX 27,1,133,78,1,0
505 FOR N=1  TO 16:NN=5*N-3:FOR M=1  TO 21:MM=5*M+25
510 ON M(M,N)+1 GOTO 530,515,525,520
515 LINE MM,NN,MM+5,NN:GOTO 530
520 LINE MM,NN,MM+5,NN
525 LINE MM,NN,MM,NN+5
530 NEXT M:NEXT N:BOX 5*X+27,5*Y-1,5*X+28,5*Y
535 K=ASC(INKEY$):DS=60*(PEEK(1016)-PM)+(PEEK(1017)-PS)/2:RETURN
540 CLS:OPEN "score" FOR RANDOM AS#1:CLOSE #1:IF LOF(1)=0 THEN 545
541 OPEN "score" FOR INPUT AS#1:INPUT #1,SMAPNUM,SMIN,SSEC,NAME$:CLOSE #1
542 IF MAPNUM<>SMAPNUM THEN 545
543 PRINT "这张地图的纪录是";NAME$;"创造的";SMIN;"分";SSEC;"秒!":GOTO 548
545 PRINT "这是张新地图,暂时没有成绩记录!"
548 K=ASC(INKEY$):RETURN
550 OPEN "map" FOR INPUT AS#1:I=1:CLS:LOCATE 3,3:PRINT "Loading..."
560 INPUT #1,MAPNUM:FOR N=1 TO 16:FOR M=1 TO 21:INPUT #1,M(M,N):NEXT M
562 IF N/2=INT(N/2) THEN I=NOT (I):BOX 105,0,160,80,1,0
563 CIRCLE 123,29+I,11,1:CIRCLE 123,27-1.5*N+I,14,1,0:CIRCLE 123,29+I,13
564 LINE 118-I,51,131+I,51:LINE 120-I,52,127+I,52
565 NEXT N:INPUT #1,EX,EY:CLOSE #1:LOCATE 3,3:PRINT "OK! Finish!"
580 FOR I=0 TO 1000:NEXT I:READED=1:RETURN
600 IF READED=0 THEN GOSUB 550
602 DATA 0,16,76,160,12,21,72,148,55,37,55,105,65,41,51,95,70,43,49,90,75,45,47,85
603 DIM P(5,3):FOR I=0 TO 5:READ P(I,0):READ P(I,1):READ P(I,2):READ P(I,3):NEXT I
605 IF EX=1 OR EX=20 THEN X=21-EX:Y=INT(RND(1)*3)+SGN(8.5-EY)*6+7
607 IF EY=1 OR EY=15 THEN Y=16-EY:X=INT(RND(1)*5)+SGN(11.5-EX)*7.5+8.5
609 D=INT(RND(1)*4)+1:DIM SL(5,1):DIM SR(5):DIM CL(1):DIM CR(1):SEC=-5
620 OPEN "nt.sys" FOR INPUT AS#1:CLS:FOR I=2 TO 7
630 INPUT #1,M:IF M=0 THEN 635
632 INPUT #1,N:LINE M,I,N,I:GOTO 630
635 INPUT #1,M:IF M=8 THEN 640
638 DRAW M,I:GOTO 635
640 NEXT I:CLOSE #1
650 IF X=EX AND Y=EY THEN 3000
655 FOR I=0 TO 4:SL(I,0)=0:SL(I,1)=0:SR(I)=0:NEXT I
660 FOR I=0 TO 1:CL(I)=0:CR(I)=0:NEXT I:FW=0:LF=0:RT=0:BW=0
670 ON D GOTO 700,750,800,850
700 FOR I=0 TO 4:IF Y+1+I=17 THEN I=4:GOTO 706
701 IF M(X+1,Y+I)>1 THEN SL(I,1)=1:GOTO 703
702 IF M(X+1,Y+I+1)=1 OR M(X+1,Y+I+1)=3 THEN SL(I,1)=2
703 IF M(X,Y+I)>1 THEN SR(I)=2:GOTO 705
704 IF M(X-1,Y+I+1)=1 OR M(X-1,Y+I+1)=3 THEN SR(I)=1
705 IF M(X,Y+I+1)=1 OR M(X,Y+I+1)=3 THEN SL(I,0)=1:I=4
706 NEXT I
708 IF M(X,Y)=1 OR M(X,Y)=3 THEN BW=1
709 IF SL(0,0)=1 OR X<2 OR Y>13 OR X>19 THEN 900
710 IF SL(1,1)<>0 THEN 720
711 IF M(X+1,Y+3)=1 OR M(X+1,Y+3)=3 THEN CL(0)=1
712 IF M(X+2,Y+2)>1 THEN CL(1)=1:GOTO 720
713 IF M(X+2,Y+3)=1 OR M(X+2,Y+3)=3 THEN CL(1)=2
720 IF SR(1)<>0 THEN 900
721 IF M(X-1,Y+3)=1 OR M(X-1,Y+3)=3 THEN CR(0)=1
722 IF M(X-1,Y+2)>1 THEN CR(1)=1:GOTO 900
723 IF M(X-2,Y+3)=1 OR M(X-2,Y+3)=3 THEN CR(1)=2
730 GOTO 900
750 FOR I=0 TO 4:IF X-I=0 THEN I=4:GOTO 755
751 SL(I,1)=M(X-I,Y+1):IF SL(I,1)=3 THEN SL(I,1)=1
752 IF M(X-I,Y)=1 OR M(X-I,Y)=3 THEN SR(I)=2:GOTO 754
753 IF M(X-I,Y-1)>1 THEN SR(I)=1
754 IF M(X-I,Y)>1 THEN SL(I,0)=1:I=4
755 NEXT I
758 IF M(X+1,Y)>1 THEN BW=1
759 IF SL(0,0)=1 OR X<2 OR Y<2 OR Y>14 THEN 900
760 IF SL(1,1)<>0 THEN 770
761 IF M(X-2,Y+1)>1 THEN CL(0)=1
762 ON M(X-2,Y+2)+1 GOTO 770,764,763,764
763 CL(1)=2:GOTO 770
764 CL(1)=1
770 IF SR(1)<>0 THEN 900
771 ON M(X-2,Y-1)+1 GOTO 773,775,772,774
772 CR(0)=1
773 IF M(X-2,Y-2)>1 THEN CR(1)=2:GOTO 900
774 CR(0)=1
775 CR(1)=1
780 GOTO 900
800 FOR I=0 TO 4:IF Y-I=0 THEN I=4:GOTO 805
801 SR(I)=M(X+1,Y-I):IF SR(I)=3 THEN SR(I)=2
802 IF M(X,Y-I)>1 THEN SL(I,1)=1:GOTO 804
803 IF M(X-1,Y-I)=1 OR M(X-1,Y-I)=3 THEN SL(I,1)=2
804 IF M(X,Y-I)=1 OR M(X,Y-I)=3 THEN SL(I,0)=1:I=4
805 NEXT I
807 IF M(X,Y+1)=1 OR M(X,Y+1)=3 THEN BW=1
808 IF SL(0,0)=1 OR X<2 OR Y<2 OR X>19 THEN 900
810 IF SL(1,1)<>0 THEN 820
811 ON M(X-1,Y-2)+1GOTO 813,812,816,815
812 CL(0)=1
813 IF M(X-2,Y-2)=1 OR M(X-2,Y-2)=3 THEN CL(1)=2
814 GOTO 820
815 CL(0)=1
816 CL(1)=1
820 IF SR(1)<>0 THEN 900
821 IF M(X+1,Y-2)=1 OR M(X+1,Y-2)=3 THEN CR(0)=1
822 ON M(X+2,Y-2)+1GOTO 900,823,824,824
823 CR(1)=2:GOTO 900
824 CR(1)=1:GOTO 900
850 FOR I=0 TO 4:IF X+I+1=22 THEN I=4:GOTO 856
851 IF M(X+I,Y)=1 OR M(X+I,Y)=3 THEN SL(I,1)=1:GOTO 853
852 IF M(X+I+1,Y-1)>1 THEN SL(I,1)=2
853 IF M(X+I,Y+1)=1 OR M(X+I,Y+1)=3 THEN SR(I)=2:GOTO 855
854 IF M(X+I+1,Y+1)>1 THEN SR(I)=1
855 IF M(X+I+1,Y)>1 THEN SL(I,0)=1:I=4
856 NEXT I
858 IF M(X,Y)>1 THEN BW=1
859 IF SL(0,0)=1 OR Y<2 OR Y>14 OR X>18 THEN 900
860 IF SL(1,1)<>0 THEN 870
861 IF M(X+3,Y-1)>1 THEN CL(0)=1
862 IF M(X+2,Y-1)=1 THEN CL(1)=1:GOTO 870
863 IF M(X+3,Y-2)>1 THEN CL(1)=2
870 IF SR(1)<>0 THEN 900
871 IF M(X+3,Y+1)>1 THEN CR(0)=1
872 IF M(X+2,Y+2)=1 OR M(X+2,Y+2)=3 THEN CR(1)=1:GOTO 900
873 IF M(X+3,Y+2)>1 THEN CR(1)=2
900 BOX 0,16,160,80,1,0
905 FOR I=0 TO 4:M=I+1
910 ON SL(I,1)+1GOTO 911,914,912
911 IF SL(I+1,1)=1 THEN 915 ELSE 920
912 BOX P(M,0),P(M,1),P(I,0),P(M,2):GOTO 920
914 LINE P(M,0),P(M,2),P(I,0),P(I,2):LINE P(M,0),P(M,1),P(I,0),P(I,1)
915 LINE P(M,0),P(M,1),P(M,0),P(M,2)
920 ON SR(I)+1GOTO 921,922,924
921 IF SR(I+1)=2 THEN 925 ELSE 930
922 BOX P(M,3),P(M,1),P(I,3),P(M,2):GOTO 930
924 LINE P(M,3),P(M,1),P(I,3),P(I,1):LINE P(M,3),P(M,2),P(I,3),P(I,2)
925 LINE P(M,3),P(M,1),P(M,3),P(M,2)
930 IF SL(I,0)=1 THEN BOX P(M,0),P(M,1),P(M,3),P(M,2):I=4
940 NEXT I:LINE 159,17,159,75,0
945 IF SL(0,0)=1 THEN 970
950 IF CL(0)=1 THEN BOX 40,41,55,51
951 ON CL(1)+1GOTO 960,952,955
952 LINE 12,37,40,41:LINE 12,55,40,51:LINE 40,41,40,51:GOTO 960
955 BOX 12,41,40,51
960 IF CR(0)=1 THEN BOX 120,41,105,51
961 ON CR(1)+1GOTO 970,962,965
962 LINE 148,37,120,41:LINE 148,55,120,51:LINE 120,41,120,51:GOTO 970
965 BOX 148,41,120,51
970 IF SL(0,0)=1 THEN FW=1
972 IF SL(0,1)=1 THEN LF=1
974 IF SR(0)=2 THEN RT=1
980 IF PE=PEEK(1137) THEN 990 ELSE PE=PEEK(1137):SEC=SEC+1
985 IF SEC>59 THEN MIN=MIN+1:SEC=SEC-60:GOTO 985
986 LOCATE 1,14:PRINT MIN;":";SEC;" "
990 K=0:WHILE PEEK(199)>128:K=ASC(INKEY$):WEND:IF K<>0 THEN 1000
995 GOTO 980
1000 IF K<>19 THEN 1020
1010 IF D=1 THEN D=4 ELSE D=D-1
1015 GOTO 650
1020 IF K<>14 THEN 1030
1025 IF D=4 THEN D=1 ELSE D=D+1
1028 GOTO 650
1030 IF K=113 THEN 460
1040 IF K=109 OR K=13 THEN GOSUB 500:SEC=SEC+INT(DS):GOTO 620
1050 IF K=20 AND FW=0 THEN K=0:GOTO 1100
1060 IF K=21 AND BW=0 THEN K=2:GOTO 1100
1070 IF K=22 AND RT=0 THEN K=1:GOTO 1100
1080 IF K=23 AND LF=0 THEN K=3:GOTO 1100 ELSE BEEP:GOTO 980
1100 ON D+K GOTO 1120,1130,1140,1110,1120,1130,1140
1110 X=X+1:GOTO 650
1120 Y=Y+1:GOTO 650
1130 X=X-1:GOTO 650
1140 Y=Y-1:GOTO 650
3000 PRINT "恭喜!","你的用时是";MIN;"分";SEC;"秒!":INKEY$
3010 CLS:OPEN "score" FOR RANDOM AS#1:CLOSE #1:IF LOF(1)=0 THEN 3040
3020 OPEN "score" FOR INPUT AS#1:INPUT #1,SMAPNUM,SMIN,SSEC,NAME$:CLOSE #1
3025 IF 60*SMIN+SSEC>60*MIN+SEC OR MAPNUM<>SMAPNUM THEN 3040
3030 PRINT "现在的纪录是";NAME$;"创造的";SMIN;"分";SSEC;"秒!","再接再厉!":INKEY$:GOTO 180
3040 TEXT:PRINT "你好厉害!创造了新纪录!","签个名吧:";:INPUT "";NAME$:GRAPH
3045 OPEN "score" FOR OUTPUT AS#1:WRITE #1,MAPNUM,MIN,SEC,NAME$:CLOSE #1:GOTO 180