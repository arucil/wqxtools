0 :
10 GRAPH
15 DIM DRN$(50),YT$(10),MA(13,13):YT$(0)="  "
20 PRINT "〓探索ADVANCE V3.0〓","⒈开始","⒉读取","×通信","×对战";:P=3.141592654 :GOTO 120
21 LOCATE 3,9 :PRINT "ＦＩＮＡＬ"
22 LOCATE 4,7 :PRINT "ＥＤＩＴＩＯＮ":BOX 49,17,160,80,0,1 :GOTO 70
40 IF PEEK(199)>128 GOTO 70
70 Q$=INKEY$
80 IF Q$="b"GOTO 230
90 IF Q$="n"GOTO 2550
120 REM EXPLORER S
130 IF TMP=0 THEN LOCATE 3,8 :PRINT "Now Loading": ELSE 210
140 OPEN "TS-DATA" FOR INPUT AS#1
150 FOR I=1  TO 10 :INPUT #1,DDN$(I):NEXT
160 FOR I=1  TO 50 :INPUT #1,DRN$(I):NEXT
170 FOR I=1  TO 10 :INPUT #1,YN$(I),YT$(I),YBX(I):NEXT
180 FOR I=1  TO 10 :INPUT #1,SZ$(I):NEXT
190 FOR I=1  TO 10 :INPUT #1,WQD$(I),WQNA(I),WQNB(I),WQCH$(I):NEXT
195 FOR I=1  TO 10 :INPUT #1,XZS(I):NEXT
196 FOR I=1  TO 10 :INPUT #1,BO$(I),KEY,ZBS:NEXT
200 CLOSE #1:TMP=1:VERS=5:MONSA=1000
210 LOCATE 3,7 :PRINT "            "
220 GOTO 21
230 CLS:INPUT "你的名字:";Q$ :NA$=LEFT$(Q$,10)
240 IF NA$=""GOTO 230 ELSE INPUT "摁输入";A
250 CLS:PRINT "    请分配点数":Q$=INKEY$:PRINT "剩余:10","⒈+血"
260 PRINT "⒉+攻","⒊+防","⒋+敏";:NL(1)=50 :NL(2)=30 :NL(3)=30 :NL(4)=30 :K=10
270 FOR I=1  TO 4 :LOCATE I+1,6 :PRINT NL(I);:NEXT:LOCATE 1,6 :PRINT K;" "
280 Q$=INKEY$
290 IF Q$="b" THEN O=1 :340
300 IF Q$="n" THEN O=2 :340
310 IF Q$="m" THEN O=3 :340
320 IF Q$="g" THEN O=4 :340
330 GOTO 280
340 NL(O)=NL(O)+INT(RND(1)*2)+3 :K=K-1
350 IF K=0 GOTO 360 ELSE GOTO 270
360 Y(1)=1 :WQN$="普通的剑⑴":WQG=16 :WQM=0 :YFN$="普通的大衣⑴":YFF=8 :YFM=0 :MON=300 :ZY=5
370 GOSUB 380 :GOTO 390
380 XNL(1)=NL(1):XNL(2)=NL(2)+WQG :XNL(3)=NL(3)+YFF :XNL(4)=NL(4)+WQM+YFM :RETURN
390 CLS:PRINT "准备出发了！":Q$=INKEY$:LV=1 :NEX=8 :ZHSN$="无":GOTO 500
500 CLS:PRINT "⒈查看     ⒍装备店","⒉买东西","⒊存档","⒋炸药店","⒌出去冒险";
505 IF LV>50 THEN LOCATE 2,12:PRINT "⒎转生所"
510 XNL(1)=NL(1):Q$=INKEY$
520 IF Q$="b"GOTO 575
530 IF Q$="n"GOTO 740
540 IF Q$="h"GOTO 870
550 IF Q$="g"GOTO 861
560 IF Q$="m"GOTO 2500
565 IF Q$="j"GOTO 2610
566 IF Q$="t" AND LV>50 THEN 2950
567 IF Q$="q" THEN CLS:PRINT "要退出了? Y/N":W$=INKEY$:IF W$="y" THEN 8000 ELSE 500 
570 GOTO 510
575 GOSUB 580 :GOTO 500
580 CLS:PRINT NA$,"等级:";LV,"经验:";JY,"下级需:";NEX,"金钱:";INT(MON);:MON=INT(MON)
584 IF VZS>0 THEN LOCATE 1,12:PRINT VZS;"次转生"
585 Q$=INKEY$:CLS:IF Q$="v" AND ZBS>ZBA THEN 586 ELSE 588
586 CLS:INPUT "等级:";LV:INPUT "经验:";JY:INPUT "升级需";NEX
587 INPUT "转生";VZS:CLS:BEEP:MON=INT(MON-(MON/2)):ZBA=ZBA+1:GOTO 580
588 CLS:PRINT "System Option":PRINT "Ｋｅｙ:";KEY:IF KEY=0 THEN 9800
589 IF ZBA>=0 THEN LOCATE 3,1:PRINT "秘籍使用:";ZBA;"/";ZBS:W$=INKEY$:PRINT "Name:";NA$
590 PRINT "ＨＰ:";XNL(1);"/";NL(1),"攻击:";XNL(2),"防御:";XNL(3),"敏捷:";XNL(4);:Q$=INKEY$
591 IF Q$="a" AND ZBS>ZBA THEN CLS:INPUT "HP:";XNL(1):INPUT "MAX HP:";NL(1): ELSE 600
592 INPUT "攻击:";XNL(2):INPUT "防御:";XNL(3):INPUT "敏捷:";XNL(4)
593 CLS:INPUT "金钱";MON:BEEP:PRINT "注:此项能力存裆/升级后自动消失!":W$=INKEY$
595 CLS:BEEP:MON=INT(MON-(MON/2)):ZBA=ZBA+1:GOTO 588
600 CLS:PRINT "【装备】",WQN$,"＋攻:";WQG;" ＋敏:";WQM,YFN$,"＋防:";YFF;" ＋敏:";YFM;
601 Q$=INKEY$:CLS:PRINT "召唤兽","【";ZHSN$;"】","等级:";ZHSL,"能力:";ZHS 
602 IF Q$="n" AND ZBS>ZBA THEN CLS:GOTO 603 ELSE 605
603 INPUT "召唤兽:";ZHSN$:INPUT "等级:";ZHSL:INPUT "能力:";ZHS:INPUT "炸药:";ZY
604 CLS:MON=INT(MON)-(MON/2):BEEP:ZBA=ZBA+1:GOTO 601 
605 Q$=INKEY$:IF Q$="s" AND MON>0 THEN 606 ELSE GOSUB 610 :RETURN
606 CLS:INPUT "武器:";WQN$:INPUT "＋攻:";WQG
607 INPUT "＋敏:";WQM:CLS:INPUT "防具:";YFN$:INPUT "＋防:";YFF:INPUT "＋敏:";YFM
608 CLS:W$=INKEY$:MON=MON-(YFF+YFM)/2:W$=INKEY$:PRINT "读档后有效！":GOTO 605
610 CLS:PRINT "【物品】  炸药×";ZY
620 FOR I=1  TO 10 :PRINT YT$(Y(I));:NEXT:U=1
630 LOCATE 3,1 :PRINT "                    ";:LOCATE 3,U*2-1 :PRINT "";
635 IF Y(U)=0 THEN LOCATE 4,1 :PRINT "无               ":GOTO 650
640 LOCATE 4,1 :PRINT YN$(Y(U));"补";YBX(Y(U));"HP    "
650 Q=ASC(INKEY$)
660 IF Q=22 THEN IF U=10 GOTO 650 ELSE U=U+1 :GOTO 630
670 IF Q=23 THEN IF U=1 GOTO 650 ELSE U=U-1 :GOTO 630
680 IF Q=13 GOTO 720
690 IF Q=27 THEN 710
700 GOTO 650
710 RETURN
720 IF Y(U)=0 GOTO 650
725 XNL(1)=XNL(1)+YBX(Y(U)):Y(U)=0
730 IF XNL(1)>NL(1) THEN XNL(1)=NL(1)
735 LOCATE 2,U*2-1:PRINT "  ":GOTO 650
740 CLS:PRINT "商店，买什么？","⒈畈菀 ￥100","⒉缪瓶 ￥500","⒊パ罐 ￥800"
750 PRINT "⒋离开";:V=0 :K=0
760 Q$=INKEY$
770 IF Q$="b" THEN V=1 :J=100 :820
780 IF Q$="n" THEN V=4 :J=500 :820
790 IF Q$="m" THEN V=6 :J=800 :820
800 IF Q$="g"GOTO 500
810 GOTO 760
820 IF MON<J THEN CLS:PRINT "钱不够！":Q$=INKEY$:740
830 K=K+1 
840 IF Y(K)=0 GOTO 860 
845 IF K=10 THEN 850 ELSE GOTO 830
850 CLS:PRINT "物品满了！","出去吧":Q$=INKEY$:GOTO 500
860 MON=MON-J :Y(K)=V :CLS:PRINT "买了";YN$(V):Q$=INKEY$:GOTO 740
861 CLS:PRINT "【炸药店】","⒈买炸药","⒉离开"
862 Q$=INKEY$
863 IF Q$="b"GOTO 866
864 IF Q$="n"GOTO 500
865 GOTO 862 
866 CLS:A=LV*2+18-VZS :PRINT "￥";A;"一个":INPUT "买几个:";B
867 IF B<=0 OR B<>INT(B) OR ZY+B>9999 THEN 861
868 IF MON<A*B THEN CLS:PRINT "钱不够":Q$=INKEY$:GOTO 861
869 MON=MON-(A*B):ZY=ZY+B :CLS:PRINT "买了";B;"个炸药":Q$=INKEY$:GOTO 861
870 FOR I=1  TO 5 :A=I*2-1 :LOCATE I,1 :PRINT SZ$(A);DDN$(A);SZ$(A+1);DDN$(A+1);:NEXT:K=1 
880 U=ASC(INKEY$)
890 IF U=XZS(K) THEN 920 
895 IF K=10 GOTO 900 ELSE K=K+1 :GOTO 890
900 IF U=27 GOTO 500 
910 GOTO 880
920 DX=K :CS=1 :WX=1 :WY=1 :CLS:PRINT "进入";DDN$(DX);"…"
950 FOR I=1  TO 13 :FOR O=1  TO 13 :MA(I,O)=INT(RND(1)+DX*0.015+CS*0.002+0.19):NEXT O :NEXT I
960 DJA=INT(RND(1)*13)+1 :DJB=INT(RND(1)*13)+1 :MA(WX,WY)=2
970 BXA=INT(RND(1)*13)+1 :BXB=INT(RND(1)*13)+1 :LA=INT(RND(1)*13)+1 :LB=INT(RND(1)*13)+1
971 IF CS/20=INT(CS/20)GOTO 2790
980 CLS:LOCATE 1,12 :PRINT DDN$(DX):LOCATE 2,11 :PRINT "层数:";CS
990 LOCATE 3,11 :PRINT "#";XNL(1);"/";NL(1):LOCATE 4,11 :PRINT "炸药:";ZY :K=0 :L=0
1000 FOR I=1  TO 13 :FOR O=1  TO 13
1010 IF MA(I,O)=2 THEN BOX I*6-5,O*6-5,I*6+1,O*6+1,0,1
1020 IF MA(I,O)=3 THEN BOX I*6-5,O*6-5,I*6+1,O*6+1,1,1
1030 NEXT O :NEXT I
1031 BOX LA*6-5,LB*6-5,LA*6+1,LB*6+1,0,1 :LINE LA*6-5,LB*6-5,LA*6+1,LB*6+1,1
1032 BOX WX*6-3,WY*6-3,WX*6-1,WY*6-1,1,1
1035 LOCATE 5,11 :PRINT "         ";
1036 LINE 80,0,80,80,1
1040 Q=ASC(INKEY$)
1050 IF Q=20 THEN A=0 :B=-1 :1160
1060 IF Q=21 THEN A=0 :B=1 :1160
1070 IF Q=22 THEN A=1 :B=0 :1160
1080 IF Q=23 THEN A=-1 :B=0 :1160
1090 IF Q=13 THEN GOSUB 580 :CLS:GOTO 980
1100 IF Q=98 GOTO 1250
1110 IF Q=27 GOTO 1130
1111 IF Q=112 THEN 9000
1112 IF Q=113 THEN 9300
1113 IF Q=119 THEN 9850
1120 GOTO 1040
1130 LOCATE 5,11 :PRINT "离开Y/N ?";:Q$=INKEY$
1140 IF Q$="y"GOTO 500
1141 IF Q$="w" AND ZBS>ZBA THEN MON=50000:BEEP
1142 IF Q$="q" THEN SV=0:GOTO 3140
1143 IF Q$="x" THEN SV=0:GOTO 3230
1150 GOTO 1035
1160 IF WY+B=14 OR WX+A=0 OR WX+A=14 OR WY+B=0 THEN 1040
1190 IF WX+A=LA AND WY+B=LB THEN 1240
1200 IF MA(WX+A,WY+B)=1 OR MA(WX+A,WY+B)=3 GOTO 1230
1210 BOX WX*6-4,WY*6-4,WX*6,WY*6,1,0 :WX=WX+A :WY=WY+B :MA(WX,WY)=2
1215 BOX WX*6-5,WY*6-5,WX*6+1,WY*6+1,0,1
1216 BOX WX*6-3,WY*6-3,WX*6-1,WY*6-1,1,1 :U=RND(1)
1217 IF WX=DJA AND WY=DJB THEN 1640
1220 IF WX=BXA AND WY=BXB THEN 1330
1223 IF BZA=1 THEN 1330
1225 IF U>0.94 THEN 1640 ELSE GOTO 1040
1230 BOX (WX+A)*6-5,(WY+B)*6-5,(WX+A)*6+1,(WY+B)*6+1,1,1 :MA(WX+A,WY+B)=3 :GOTO 1040
1240 CS=CS+1 :CLS:PRINT "前往下一层…":GOTO 950
1250 IF ZY=0 THEN BEEP:GOTO 1040
1260 ZY=ZY-1 
1270 IF WX-1=0 GOTO 1280 ELSE MA(WX-1,WY)=2 :BOX (WX-1)*6-4,WY*6-4,(WX-1)*6,WY*6,1,0
1280 IF WX+1=14 GOTO 1290 ELSE MA(WX+1,WY)=2 :BOX (WX+1)*6-4,WY*6-4,(WX+1)*6,WY*6,1,0
1290 IF WY-1=0 GOTO 1300 ELSE MA(WX,WY-1)=2 :BOX WX*6-4,(WY-1)*6-4,WX*6,(WY-1)*6,1,0
1300 IF WY+1=14 GOTO 1310 ELSE MA(WX,WY+1)=2 :BOX WX*6-4,(WY+1)*6-4,WX*6,(WY+1)*6,1,0
1310 LOCATE 4,16 :PRINT ZY;" "
1315 LOCATE 5,11 :PRINT "使用炸药";:Q$=INKEY$
1320 GOTO 1035
1330 CLS:PRINT "发现一个宝箱，里面是…":Q$=INKEY$:V=INT(1+CS*0.05):GOSUB 1340 
1335 BXA=0 :BXB=0 :BZA=0:GOTO 980
1340 A=DX+INT(RND(1)*3)-1 :IF BOSS>0 GOTO 1360
1350 IF A>10 OR A<1 GOTO 1340
1360 B=INT(RND(1)*10)+1 :R=0
1370 FOR I=1  TO 9+V
1380 R=R+RND(1)*3+0.2 :NEXT
1390 C=INT(R)-15
1395 IF BOSS>0 THEN A=A+1+BOSS+VZS
1400 IF C<=0 THEN C=1
1410 IF C>10 AND BOSS=0 THEN C=10
1420 E=WQNA(B)*A :F=WQNB(B)*A
1430 E=INT(E+E*C/10)+INT(RND(1)*A):F=INT(F+F*C/10)+INT(RND(1)*A/2)
1435 IF BOSS>0 THEN I$=DN$+"之"+WQD$(B):BOSS=0 :1450
1440 I$=WQCH$(C)+WQD$(B)+SZ$(A)
1450 CLS:PRINT "得到【";I$;"】":Q$=INKEY$:CLS
1460 IF B>=6 GOTO 1560
1470 PRINT "[得到]";I$,"＋攻:";E;" ＋敏:";F,"[原来]";WQN$
1480 PRINT "＋攻:";WQG;" ＋敏";WQM,"换吗?(Y/N)";
1490 Q$=INKEY$
1500 IF Q$="y"GOTO 1550
1510 IF Q$="n"GOTO 1540
1520 GOTO 1490
1540 CLS:PRINT "你扔掉了新装备…":Q$=INKEY$:CLS:RETURN
1550 CLS:PRINT "你换上了新装备":WQN$=I$ :WQG=E :WQM=F :GOSUB 380 :Q$=INKEY$:RETURN
1560 PRINT "[得到]";I$,"＋防:";E;" ＋敏:";F,"[原来]";YFN$
1570 PRINT "＋防:";YFF;" ＋敏";YFM;,"换吗?(Y/N)";
1590 Q$=INKEY$
1600 IF Q$="y"GOTO 1630
1610 IF Q$="n"GOTO 1540
1620 GOTO 1590
1630 CLS:PRINT "你换上了新装备":YFN$=I$ :YFF=E :YFM=F :GOSUB 380 :Q$=INKEY$:RETURN
1640 U=DX*5-4+INT(RND(1)*5):DN$=DRN$(U):DLV=INT(DX*10-9+CS*0.25+RND(1)*3)
1650 DHP=INT(DLV*4+30+RND(1)*DLV*2):DG=INT(DLV*4+20+RND(1)*(DLV+5))
1655 DF=INT(DLV*3.5+10+RND(1)*(DLV+5))
1660 DM=INT(DLV*3.5+RND(1)*(DLV+5)):DH=DHP
1670 DWQ=INT(DLV*3+RND(1)*(DLV+2))
1680 DYF=INT(DLV*1.5+RND(1)*(DLV+2)):O=0
1700 FOR I=0  TO 80 STEP 0.5
1710 LINE 1,I,160-I*2,I,1 :LINE I*2,80-I,160,80-I,1 :NEXT
1720 CLS:PRINT NA$ :LOCATE 1,21-LEN(DN$):PRINT DN$
1730 BOX 1,20,71,26,0,1 :BOX 88,20,160,26,0,1 :BOX 1,21,XNL(1)/NL(1)*70,25,1,1 
1735 BOX 88,21,DHP/DH*70+88,25,1,1
1740 LOCATE 3,1 :PRINT "⒈攻⒉召⒊物⒋封⒌逃"
1750 Q$=INKEY$
1760 IF Q$="b" THEN O=0 :1811
1770 IF Q$="n"GOTO 2010
1780 IF Q$="m" THEN GOSUB 610 :GOTO 1720 
1790 IF Q$="g"GOTO 2100
1800 IF Q$="h"GOTO 2060
1801 IF Q$="j" THEN PRINT "瞬间移动!":W$=INKEY$:BOSS=0:V=V-1:MON=100:GOTO 980
1802 IF Q$="s" THEN PRINT "最终必杀!":W$=INKEY$:V=V-VERS:MON=MON-MONSA:HPJ=9999:GOTO 1835
1810 GOTO 1750
1811 IF DM>XNL(4) THEN R=1 :1900
1820 BOX 1,48,160,80,1,0 :U=(XNL(4)-DM)/3+80 :Y=RND(1)*100 :GJ=WQG+O
1821 IF U<10 THEN U=10
1822 IF Y>U THEN 1823 ELSE GOTO 1830
1823 LOCATE 4,1 :PRINT "你攻击","ＭＩＳＳ";:Q$=INKEY$:GOTO 1886
1830 KKL=XNL(4)*RND(1)*RND(1):IF CS=0 THEN HPJ=V*10:GOTO 1835
1831 IF KKL>DM THEN LOCATE 4,1 :PRINT "你对";DN$;"发动必杀一击";:GJ=GJ*2
1832 Q$=INKEY$:HPJ=INT(((LV*0.4+2)*GJ*XNL(2)/DF/DYF+2)*(RND(1)*0.4+0.7))
1835 IF HPJ>9999 THEN HPJ=9999
1840 K=DHP/DH*70+88 :DHP=DHP-HPJ :O=0
1841 IF DHP<0 THEN DHP=0
1842 L=DHP/DH*70+89
1850 LOCATE 4,1 :PRINT "你对";DN$;"攻击，";DN$;"费";HPJ;"HP";
1860 FOR I=K  TO L STEP -0.1
1870 LINE I,21,I,25,0 :NEXT
1875 IF DHP=0 GOTO 2200
1880 Q$=INKEY$
1886 IF R=1 THEN R=0 :1720
1900 BOX 1,48,160,80,1,0 :U=(DM-XNL(4))/3+80 :Y=RND(1)*100 :CT=DG
1901 IF U<10 THEN U=10
1902 IF Y>U THEN 1903 ELSE GOTO 1905
1903 LOCATE 4,1 :PRINT DN$;"攻击","ＭＩＳＳ";:Q$=INKEY$
1904 IF R=1 GOTO 1820 ELSE GOTO 1720
1905 IF DM*RND(1)*RND(1)>XNL(4) THEN LOCATE 4,1 :PRINT DN$;"对你发动必杀一击"; ELSE GOTO 1907
1906 CT=CT*2:Q$=INKEY$
1907 HPJ=INT(((DLV*0.4+2)*CT*DWQ/XNL(3)/YFF+2)*(RND(1)*0.4+0.7))
1910 IF HPJ>9999 THEN HPJ=9999
1920 K=XNL(1)/NL(1)*70 :XNL(1)=XNL(1)-HPJ :O=0 :CT=0
1930 IF XNL(1)<0 THEN XNL(1)=0
1940 L=XNL(1)/NL(1)*70+1
1950 LOCATE 4,1 :PRINT DN$;"对你攻击，你费";HPJ;"HP";
1960 FOR I=K  TO L STEP -0.5 
1970 LINE I,21,I,25,0 :NEXT
1980 IF XNL(1)=0 GOTO 2000 
1990 Q$=INKEY$
1995 IF R=1 GOTO 1820 ELSE GOTO 1720
2000 IF HPJ>NL(1)*2 THEN LOCATE 2,1 :PRINT " FLY OUT! ":Q$=INKEY$ ELSE Q$=INKEY$
2005 CLS:PRINT "你眼一黑，倒在地上…":Q$=INKEY$:BOSS=0 :GOTO 500
2010 IF ZHSL=0 OR XNL(1)<NL(1)/10 THEN BEEP:GOTO 1750
2020 U=INT(NL(1)/10):LOCATE 4,1 :PRINT "你召唤";ZHSN$;"与你合击。HP费";U;
2030 K=XNL(1)/NL(1)*70 :XNL(1)=XNL(1)-U :L=XNL(1)/NL(1)*70+1
2040 FOR I=K  TO L STEP -0.02
2045 LINE I,21,I,25,0 :NEXT
2050 Q$=INKEY$:O=ZHS :GOTO 1811
2060 LOCATE 4,1 :PRINT "你准备逃跑…"
2070 I=(XNL(4)-DM)/3+50 :U=RND(1)*100 :Q$=INKEY$
2080 IF U>I THEN LOCATE 4,1 :PRINT "被";DN$;"追上了！":Q$=INKEY$:GOTO 1900
2090 LOCATE 4,1 :PRINT "你成功逃离了战斗":Q$=INKEY$:BOSS=0 :GOTO 980
2100 LOCATE 4,1 :PRINT "你准备封印";DN$ :I=(1-DHP/DH)*80-DLV/2 :U=RND(1)*100 :Q$=INKEY$
2101 IF VZS>0 AND I<10 THEN I=(1-DHP/DH)*10*VZS
2110 IF U>I THEN LOCATE 4,1 :PRINT DN$;"挣脱出来！":Q$=INKEY$:GOTO 1900
2120 LOCATE 4,1 :PRINT "你成功封印了";DN$ :Q$=INKEY$
2130 CLS:PRINT "【封印】";DN$,"等级";DLV;" 能力";DWQ
2140 PRINT "【原来】";ZHSN$,"等级";ZHSL;" 能力";ZHS;,"换吗?(Y/N)";
2150 Q$=INKEY$
2160 IF Q$="y"GOTO 2190
2170 IF Q$="n"GOTO 2295 ELSE GOTO 2150
2190 CLS:PRINT "你得到了新的召唤兽":ZHSN$=DN$ :ZHSL=DLV :ZHS=DWQ :Q$=INKEY$:GOTO 2295
2200 IF HPJ>DH*2 THEN LOCATE 2,11 :PRINT " FLY OUT! ":Q$=INKEY$ ELSE Q$=INKEY$
2205 I=DLV*4+INT((RND(1)+0.8)*LV/5):O=DLV*4+INT((RND(1)+0.8)*LV/2):JY=JY+I :MON=MON+O
2210 CLS:PRINT "战斗胜利！","得到经验:";I,"得到钱:";O :Q$=INKEY$
2220 IF JY>=NEX GOTO 2310
2230 I=RND(1)
2240 IF I>0.85 THEN O=DX+INT(RND(1)*3)-1 ELSE GOTO 2290 
2245 IF O<1 THEN O=1
2246 IF O>10 THEN O=10
2250 CLS:PRINT "得到";YT$(O);YN$(O):I=0 :Q$=INKEY$
2260 I=I+1
2270 IF Y(I)=0 THEN Y(I)=O :2290
2280 IF I=10 THEN 2290 ELSE GOTO 2260
2290 IF WX=DJA AND WY=DJB THEN V=1 :DJA=0 :DJB=0 :GOSUB 1340
2295 IF BOSS=101 GOTO 3200
2296 IF BOSS=102 GOTO 3290
2297 IF BOSS>0 THEN V=BOSS :GOSUB 1340
2300 GOTO 980
2310 CLS:PRINT "你升级了","HP+","攻+","防+","敏+";
2320 FOR I=1  TO 4
2325 K=INT(RND(1)*2)+2+VZS
2330 LOCATE I+1,4 :PRINT K;
2335 NL(I)=NL(I)+K
2340 NEXT:K=4+VZS
2350 Q$=INKEY$:IF Q$="q" THEN BEEP:CLS:GOTO 980
2360 CLS:PRINT "分配剩余:";K,"⒈+血:";NL(1),"⒉+攻:";NL(2),"⒊+防:";NL(3),"⒋+敏:";NL(4);
2370 Q$=INKEY$
2380 IF Q$="b" THEN O=1 :2430
2390 IF Q$="n" THEN O=2 :2430
2400 IF Q$="m" THEN O=3 :2430
2410 IF Q$="g" THEN O=4 :2430
2420 GOTO 2370
2430 P=INT(RND(1)*2)+2+VZS :NL(O)=NL(O)+P
2440 LOCATE O+1,7 :PRINT NL(O);:K=K-1
2450 LOCATE 1,10 :PRINT K 
2460 IF K=0 GOTO 2470 ELSE GOTO 2370
2470 GOSUB 380 
2480 LV=LV+1 :I=LV+1 :NEX=I*I*I
2490 GOTO 2220
2500 CLS:PRINT "★存档选择★","⒈存档Ａ","⒉存档Ｂ","⒊存档Ｃ"
2501 Q=ASC(INKEY$)
2502 IF Q=98 THEN I$="SAVE-A":2509
2503 IF Q=110 THEN I$="SAVE-B":2509
2504 IF Q=109 THEN I$="SAVE-C":2509
2505 IF Q=27 GOTO 500
2506 GOTO 2501
2509 CLS:PRINT "存档中…";
2510 OPEN I$ FOR OUTPUT AS#1
2515 WRITE #1,NA$,ZHSN$,ZHS,ZHSL
2516 FOR I=1  TO 10 :WRITE #1,Y(I):NEXT
2520 WRITE #1,NL(1),NL(2),NL(3),NL(4),WQN$,WQG,WQM,MON,ZY,LV,JY,NEX,YFN$,YFF,YFM,VZS,ZBA
2530 CLOSE #1
2540 CLS:GOTO 500
2550 CLS:PRINT "★读档选择★","⒈存档Ａ","⒉存档Ｂ","⒊存档Ｃ"
2551 Q=ASC(INKEY$)
2552 IF Q=98 THEN I$="SAVE-A":2557
2553 IF Q=110 THEN I$="SAVE-B":2557
2554 IF Q=109 THEN I$="SAVE-C":2557
2555 IF Q=27 THEN 20 ELSE GOTO 2551
2557 PRINT "读取中…";:OPEN I$ FOR INPUT AS#1
2558 INPUT #1,NA$,ZHSN$,ZHS,ZHSL
2559 FOR I=1  TO 10 :INPUT #1,Y(I):NEXT
2560 INPUT #1,NL(1),NL(2),NL(3),NL(4),WQN$,WQG,WQM,MON,ZY,LV,JY,NEX,YFN$,YFF,YFM,VZS,ZBA
2570 CLOSE #1
2580 GOSUB 380
2600 GOTO 2860
2610 CLS:PRINT "★装备精品店★","看看这个！"
2620 I=INT(LV/10)+1 :O=INT(RND(1)*10)+1 :P=INT(RND(1)*3)+1
2625 IF I>10 THEN I=10
2630 E=WQNA(O)*I :F=WQNB(O)*I
2640 E=INT(E+E*P/10)+INT(RND(1)*I):F=INT(F+F*P/10)+INT(RND(1)*I/2)
2650 I$=WQCH$(P)+WQD$(O)+SZ$(I):K=INT(JY/10)+300+P*P*I*I*40+INT(RND(1)*100)
2660 PRINT "【";I$;"】"
2670 IF O>5 THEN PRINT "+防:";E ELSE PRINT "+攻:";E
2680 PRINT "+敏:";F;:Q$=INKEY$:CLS
2690 PRINT K;"不二价!","⒈买了！","⒉不要"
2700 Q$=INKEY$
2710 IF Q$="b"GOTO 2740
2720 IF Q$="n" THEN CLS:PRINT "不要吗…":Q$=INKEY$:GOTO 500
2730 GOTO 2700
2740 IF MON<K GOTO 2780
2745 IF O>5 GOTO 2760
2750 WQN$=I$ :WQG=E :WQM=F :MON=MON-K :GOTO 2770
2760 YFN$=I$ :YFF=E :YFM=F :MON=MON-K
2770 CLS:PRINT "成交！":GOSUB 380 :Q$=INKEY$:GOTO 500
2780 CLS:PRINT "钱不够！":Q$=INKEY$:GOTO 500
2790 DN$=BO$(DX):DLV=DX*5*CS/2 :A=DX*CS :BOSS=CS/20+DX
2800 DHP=INT(A*4+30+RND(1)*A*2):DG=INT(A*4+20+RND(1)*(A+5))
2810 DF=INT(A*3+10+RND(1)*(A+5))
2820 DM=INT(A*3+10+RND(1)*(A+5)):DH=DHP
2830 DWQ=INT(A*3+RND(1)*(A+2))
2840 DYF=INT(A*1.5+RND(1)*(A+2)):O=0:IF DX=10 AND CS=40 GOTO 3140
2841 IF DX=9 AND CS=60 AND WQG=5000 GOTO 3230
2850 GOTO 1700
2860 IF MON>JY*2+1000 THEN A=1
2870 B=XNL(1)+XNL(2)+XNL(3)+XNL(4)
2880 IF LV*40+300<B THEN A=1
2890 IF T=123 AND VZS=0 THEN NL(1)=1 :NL(2)=0 :NL(3)=0 :NL(4)=0 ELSE GOTO 500
2900 NA$="作弊者":WQN$="无":WQG=1 :WQM=1 :ZHSN$="无":ZHSL=0 :ZHS=0
2920 YFN$="无":YFF=1 :YFM=1:MON=0
2930 GOSUB 380
2940 GOTO 2502
2950 CLS:PRINT "你真的要转生吗？","[Y/N]":Q=0
2960 FOR I=1  TO 10:IF Y(I)=I THEN Q=Q+1
2970 NEXT:Q$=INKEY$
2980 IF Q$<>"y"GOTO 500
2990 IF Q=10 GOTO 3030
3000 CLS:PRINT "请按顺序收集以下物品"
3010 FOR I=1  TO 10:PRINT YT$(I);:NEXT
3020 PRINT "你才可以转生":Q$=INKEY$:IF Q$="y" THEN 3060 ELSE GOTO 500
3030 CLS:PRINT "你要将装备作为祭品吗[Y/N]":FOR I=1  TO 4:NL(I)=INT(NL(I)/10)+50:NEXT
3040 NL(1)=NL(1)+50:Q$=INKEY$
3050 IF Q$<>"y"GOTO 3080
3060 NL(2)=NL(2)+INT(WQG/10):NL(3)=NL(3)+INT(YFF/10):NL(4)=NL(4)+INT((WQM+YFM)/10)
3070 WQN$="转生之剑":WQG=10:WQM=10:YFN$="转生之衣":YFF=10:YFM=10
3080 REM ZHSN$="真・"+ZHSN$:ZHS=INT(ZHS*1.5):ZHSL=INT(ZHSL*1.5)
3090 LV=1:JY=0:NEX=8:VZS=VZS+1:GOSUB 380
3100 CLS:PRINT "你走入转生之门","愿你的梦想与心灵同在"
3110 FOR I=1  TO 10:Y(I)=0:NEXT:Q$=INKEY$
3120 FOR I=1  TO 120:ELLIPSE 80,40,I,I/2,1,2:NEXT
3130 GOTO 500
3140 CLS
3150 PRINT "你面前是一位手持银色巨枪的骑士，身后有一匹银色的巨龙":Q$=INKEY$:CLS
3160 PRINT "骑士说:这就是无底深渊吗?好吧!黑暗之源,我们该作个了解了!":Q$=INKEY$:CLS
3170 FOR I=1  TO 160:LINE I,0,I,81,2:LINE 161-I,0,161-I,81,2:NEXT
3180 DLV=999:BOSS=101:DHP=10000:DH=DHP:DG=1000:DWQ=5000:DF=800:DYF=800:DM=2000:DN$="Huma"
3190 GOTO 1720
3200 I$="真・龙枪":E=5000:F=-100:BOSS=0:CLS
3210 :
3220 IF SV=0 THEN GOSUB 1450:GOTO 980: ELSE 8051
3230 CLS
3240 PRINT "葛妮斯:你手里的武器是…龙枪！","是你!杀了我最爱的人!!"
3250 DLV=5000:BOSS=102:DHP=9999:DH=DHP:DG=1000:DWQ=2000:DF=1:DYF=1:DM=9000:DN$="Gwyneth"
3260 Q$=INKEY$:CLS
3270 FOR I=1  TO 160:LINE I,0,I,81,2:LINE 161-I,0,161-I,81,2:NEXT
3280 CLS:GOTO 1720
3290 I$="银龙葛妮丝":E=2000:F=1000:BOSS=0:CLS
3300 :
3310 IF SV=1 THEN 8052: ELSE GOSUB 1560:GOTO 980
8000 PRINT "〓探索ADVANCE V3.0〓","⒈开始","⒉继续","⒊通信","⒋对战";:
8001 LOCATE 3,7 :PRINT "            "
8002 LOCATE 3,9 :PRINT "ＦＩＮＡＬ"
8003 LOCATE 4,7 :PRINT "ＥＤＩＴＩＯＮ":BOX 49,17,160,80,0,1 :GOTO 8010
8004 IF PEEK(199)>128 GOTO 8010
8005 REM Battle
8010 Q$=INKEY$:IF Q$="q" THEN BEEP:CLEAR:CLS:PRINT "Bye bye!":END
8011 IF Q$="b" THEN CLS:GOTO 230
8012 IF Q$="n" THEN CLS:GOTO 500
8013 IF Q$="m" THEN CLS:BEEP:PRINT "红外系统建设中......":W$=INKEY$:CLS:GOTO 8000
8014 IF Q$="g" THEN CLS:PRINT "对战场 V0.1":INKEY$:GOTO 8050
8015 GOTO 8000
8050 SV=1:GOTO 3140
8051 SV=1:GOTO 3230
8052 MON=MON*2:V=V+1:GOTO 500
9000 TEXT:CLS:PRINT "欢迎使用随意门！":PRINT "层数<9999"
9001 IF MON<50 THEN MON=0:PRINT "您的电梯出了点故障!":W$=INKEY$:GOTO 980
9002 INPUT "层数:";CS:GRAPH:GOTO 9008
9008 IF CS=0 THEN PRINT "电梯爆炸!强制进入?":W$=INKEY$:IF W$="n" THEN GOTO 9019 ELSE 9015 
9010 IF CS>ABS(9999) THEN 9000 ELSE IF CS<0 AND MON>1000 THEN 9017
9015 CS=ABS(CS):CS=INT(CS)-1:MON=MON-50:GOTO 9020
9017 GRAPH:PRINT "想去地下";ABS(CS);"层","Y/N":Q$=INKEY$:IF Q$="y" THEN 9018 ELSE 9015
9018 CS=ABS(CS):CS=INT(CS)+1:MON=MON-1000:CS=-CS:GOTO 9020
9019 IF MON>500 THEN CS=0:W$=INKEY$:MON=MON-500:GOTO 9020
9020 GRAPH:GOTO 1240
9300 CLS:PRINT "欢迎使用搜索器!"
9305 IF MON<500 THEN BEEP:PRINT "您的金钱不够！":W$=INKEY$:GOTO 980
9310 PRINT "您要搜索";CS;"层吗?","Yes/No":W$=INKEY$:TSFX=INT(RND(1)*100)
9320 IF W$="y" THEN 9325 ELSE GOTO 980
9325 CLS
9330 FOR I=1  TO 100:LOCATE 1,1:PRINT "搜索";I;"号区域"
9340 NEXT I
9345 CLS:PRINT "藏宝线索":PRINT "宝藏在第";TSFX;"区"
9346 PRINT "发掘成功!"
9350 W$=INKEY$:MON=MON-500:BZA=1:CLS:GOTO 980
9800 :GRAPH:PRINT "请您使用专用程序安装!","Bas:TS-SETUP!"
9810 W$=INKEY$:POKE 199,255
9850 CLS:PRINT "欢迎使用藏宝图":PRINT "您目前在";DDN$(DX);"","第";CS;"层"
9860 IF MON<INT(ABS(CS)) THEN PRINT "藏宝图消失了！":W$=INKEY$:CLS:GOTO 980
9861 IF RND(1)*10>5 OR MON<200 THEN PRINT "藏宝图消失了！":W$=INKEY$:CLS:GOTO 980
9870 PRINT "你打开藏宝图","发现上面布满了神秘的文字:";CHR$(BXA);CHR$(BXB);CHR$(BXA+BXB)
9871 W$=INKEY$:CLS:PRINT "是否启动翻译程序？","MONEY=-200","Yes/No"
9872 W$=INKEY$:IF W$="y" THEN 9873 ELSE 9885
9873 CLS:PRINT "(";BXA;",";BXB;")":PRINT "你在(";WX;",";WY;")":W$=INKEY$:MON=MON-200:CLS
9874 IF BXA=0 THEN PRINT "宝藏早就没了!":W$=INKEY$:GOTO 980 : ELSE GOTO 980
9885 PRINT "我能看懂!Bye!":W$=INKEY$:GOTO 980