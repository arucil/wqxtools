4 CLS:CLEAR
5 GRAPH:A$="━━━━━━━━━━":M$(0)="黄":M$(1)="金":M$(2)="战":M$(3)="役"
6 CLS:PRINT A$,"     百战天虫II"," ",A$
7 LOCATE 3,7:FOR J=56  TO 104 STEP 16:BZX=J:BZY=40:GOSUB 500:PRINT M$(K);:K=K+1
8 NEXT J
9 LOCATE 5,14:PRINT CHR$(17);"Gzz";CHR$(16);:INKEY$
11 CLEAR:DIM B$(17):MONEY=300
12 B$(0)="0000":B$(1)="0001":B$(2)="0010":B$(3)="0011":B$(4)="0100":B$(5)="0101":B$(6)="0110"
13 B$(7)="0111":B$(8)="1000":B$(9)="1001":B$(10)="1010":B$(11)="1011":B$(12)="1100"
14 B$(13)="1101":B$(14)="1110":B$(15)="1111"
15 CLS:LOCATE 2,7:PRINT "开始游戏":LOCATE 3,7:PRINT "道具商店":BOX 1,0,159,80
16 LOCATE 4,7:PRINT "输入密码"
19 MY=2
20 BOX 30,2,45,78,1,0:LOCATE MY,5:PRINT CHR$(16)
21 KV=ASC(INKEY$):IF KV=27 THEN CLEAR:CLS:END
22 IF KV=20 THEN LOCATE MY,5:PRINT " ":MY=MY-1
23 IF KV=21 THEN LOCATE MY,5:PRINT " ":MY=MY+1
24 IF KV=13 THEN GOTO 31
26 IF MY>4 OR MY<2 THEN MY=ABS(4-MY)+1
30 GOTO 20
31 IF MY=2 THEN K=1:650
32 IF MY=3 THEN 35
33 CLS:PRINT "PASSWORD:":INPUT PW$:IF PW$="GZZ" THEN K=1:BAO=1:650
34 GOTO 15
35 CLS:PRINT "买什么?","A.双发弹";SFK;" B.传送弹";CK,"C.大补丸";DK;" D.威力弹";WK,MONEY;"G"
37 A$=INKEY$
38 IF A$="a" AND MONEY>99 AND SFK<9 THEN MONEY=MONEY-100:SFK=SFK+1:35
39 IF A$="b" AND MONEY>79 AND CK<9 THEN MONEY=MONEY-80:CK=CK+1:35
40 IF A$="c" AND MONEY>49 AND DK<9 THEN MONEY=MONEY-50:DK=DK+1:35
41 IF A$=CHR$(27) THEN 15
42 IF A$="d" AND MONEY>59 AND WK<9 THEN MONEY=MONEY-60:WK=WK+1:35
45 GOTO 37
47 LOCATE 3,2:PRINT "攻击力:":LOCATE 3,11:PRINT "生命值:":LOCATE 4,2:PRINT "特点:"
48 GOTO 55
49 LOCATE 2,10:PRINT D$:LOCATE 3,9:PRINT GJ:LOCATE 3,18:PRINT BL:LOCATE 4,7:PRINT T$
50 BOX 1,10,159,80:LOCATE 1,2:PRINT "选择天虫:":KV=ASC(INKEY$):IF KV=13 THEN GOTO 510
51 LOCATE 3,9:PRINT "  ":HG=0
52 IF KV=22 THEN K=K+1
53 IF KV=23 THEN K=K-1
54 IF K>2 OR K<1 THEN K=ABS(2-K)
55 IF K=1 THEN GJ=10:BL=22:D$="":T$="生命力顽强   ":49
56 IF K=2 THEN GJ=8:HG=1:BL=20:D$="":T$="双弹同时攻击 ":49
58 GOTO 50
60 LINE 6,62,34,62:ELLIPSE 20,62,14,8:BOX 39,62,63,65:BOX 39,74,61,77:BOX 39,68,69,71
61 BOX 129,62,153,65:BOX 9,74,31,77:LINE WX,WY-5,WX,WY-10:LINE WX,WY-5,WX-2,WY-7
62 DF=0:DIR=1:EY=YM:EX=INT(RND(2)*XM+XN):GOSUB 420:EBL=22:ST=0.715:POW=0:YDL=20
63 GOSUB 390:BOX 40,69,68,70,1,0:IF BL<1 THEN LOCATE 1,1:PRINT "BAD LUCK!挂了!":INKEY$:11
64 BOX 40,62,62,65,1,0:BOX 40,63,40+BL,64:BOX 130,62,152,65,1,0:BOX 130,63,130+EBL,64
65 GOSUB 410:BOX 40,74,60,77,1,0:BOX 40,75,40+YDL,76:GOSUB 391
66 VX=DIR*COS(-ST)*8:VY=SIN(-ST)*8:ELLIPSE 20,62,13,7,1,0
67 LINE 7,62,33,62:LINE 20,62,20+VX,62+VY
68 LOCATE 5,11:PRINT INT(ST*57.3)+1;
69 KV=ASC(INKEY$):IF KV=22 OR KV=23 THEN CIRCLE WX,WY,2,1,0:DIR=SGN(22.5-KV):GOSUB 334
70 IF KV=22 AND YDB=0 AND M$="0" THEN LO=0:WX=WX+2:YDL=YDL-2:88
71 IF KV=23 AND YDB=0 AND M$="0" THEN LO=0:WX=WX-2:YDL=YDL-2:89
72 IF KV=20 THEN ST=ST+0.02:86
73 IF KV=21 THEN ST=ST-0.02:85
74 IF KV=32 THEN 600
76 IF KV=27 THEN 350
77 IF KV=ASC("s") AND SF=0 AND SFK>0 THEN S$="双发攻击!":GOSUB 400:SF=1:SFK=SFK-1:GOTO 69
78 IF KV=ASC("c") AND CS=0 AND CK>0 THEN S$="传送弹!":GOSUB 400:CS=1:CK=CK-1:GOTO 69
79 IF KV=ASC("d") AND BL<23 AND DK>0 THEN S$="大补丸!":GOSUB 400:BL=BL+5:DK=DK-1:GOTO 63
80 IF KV=ASC("w") AND WL=0 AND WK>0 THEN S$="威力加强!":GOSUB 400:WL=GJ/2:WK=WK-1:GOTO 69
81 IF KV=31 THEN 360
85 IF ST<0.4 THEN ST=0.4
86 IF ST>1.57 THEN ST=1.57
87 IF POW>14 THEN POW=14
88 IF WX>158 THEN WX=158
89 IF WX<POW THEN WX=POW
90 IF YDL<1 THEN YDB=1 ELSE YDB=0
100 GOTO 65
109 VX=VX/8*POW:VY=VY/8*POW
110 BX=WX+VX:BY=WY+VY:KBY=BY
115 IF HG=1 THEN BZ=0:KX=BX:KY=BY:KVX=VX:KVY=VY:KB=1:430 ELSE BZ=1
120 IF BY>2 AND BY<53 AND BY-VY>2 THEN CIRCLE BX-VX,BY-VY,2,1,0:CIRCLE BX,BY,2,1
122 IF BY<2 THEN BOX 1,0,159,10,1,0
126 IF BAO=1 AND VY>0 AND QB=0 THEN QB=1:VY=(EY-BY)/5:VX=(EX-BX)/5
128 IF QB=0 THEN VY=VY+1:VX=VX+WD
129 BX=BX+VX:BY=BY+VY:IF BY>53 THEN CIRCLE BX-VX,BY-VY,3,1,0:200
132 IF BX>159 OR BX<2 THEN 199
133 GOSUB 338
134 IF CS=0 AND M$="1" THEN GOSUB 497:GOTO 180
136 IF M$="1" AND CS=1 THEN CS=0:WX=BX:WY=BY-VY:CIRCLE BX-VX,BY-VY,2,1,0:200
150 GOTO 120
180 IF BX>EX-7 AND BX<EX+7 AND BY>EY-6 AND BY<EY+6 THEN JZ=1:EBL=EBL-GJ+ABS(BX-EX)-WL
181 CIRCLE BX-VX,BY-VY,2,1,0:GOTO 200
182 IF KX>EX-7 AND KX<EX+7 AND KBY>EY-7 AND KBY<EY+7 THEN EBL=EBL-GJ+ABS(KX-EX)-WL
183 CIRCLE KOX,KDY,2,1,0:GOTO 430
197 IF KDY>2 AND KY>2 THEN CIRCLE KOX,KDY,2,1,0:CIRCLE KOX,KOY,2,1,0
198 ON KB GOTO 200,63
199 IF BY-VY>0 THEN CIRCLE BX-VX,BY-VY,2,1,0
200 REM 敌开炮
202 IF EBL<1 THEN CM=(30-DF)*10:LOCATE 1,1:PRINT "干的好!奖你";CM;"G";"!继续努力!" ELSE 206
203 INKEY$:HG=0:BAO=0:SF=0:CS=0:MONEY=MONEY+CM:GOTO 15
206 IF SF=1 THEN SF=0:VX=DIR*COS(-ST)*POW:VY=SIN(-ST)*POW:QB=0:WL=0:110
207 CS=0:QB=0:WL=0:EBZ=0:YDL=20:POW=0:BZ=0:WIND=INT(RND(1)*20-10):WD=WIND/25
208 GOSUB 391:CIRCLE EX,EY,2,1,0:GOSUB 420:GOSUB 390
209 BOX 10,75,30,76,1,0:BOX 20,75,20+WIND,76
210 IF LO=0 THEN PO=RND(1)*4+ABS(EX-WX)/13
211 IF EY>WY AND LO=0 THEN PO=PO+(EY-WY)/10+1
215 IF LO=1 OR ABS(WX-EX)<20 THEN S$="嘿嘿!双发":GOSUB 400:SF=1
220 KVX=SGN(WX-EX)*0.8*PO:KVY=-0.6*PO:KX=EX+KVX:KY=EY+KVY:KBY=KY:KB=2:GOTO 430
320 LO=0:IF KX>WX-6 AND KX<WX+6 AND KY>WY-6 AND KY<WY+6 THEN LO=1:BL=BL-8+ABS(KX-WX)
321 CIRCLE KOX,KOY,2,1,0:DF=DF+1:IF SF THEN SF=0:BZ=0:220 ELSE 63
324 IF KX>WX-6 AND KX<WX+6 AND KBY>WY-6 AND KBY<WY+6 THEN BL=BL-8+ABS(KX-WX)
325 CIRCLE KOX,KDY,2,1,0:GOTO 430
334 PX=INT(WX+DIR*3):PY=INT(WY+1):GOTO 340
336 PX=INT(EX):PY=INT(EY)+3:GOTO 340
337 PX=INT(WX):PY=INT(WY)+3:GOTO 340
338 IF BY>2 THEN PX=INT(BX)+SGN(VX)*3:PY=INT(BY)+SGN(VY)*3:GOTO 340 ELSE GOTO 342
339 IF KY>2 THEN PX=INT(KX)+SGN(KVX)*3:PY=INT(KY*BZ+KBY*(1-BZ))+SGN(KVY)*3 ELSE 342
340 PN=PEEK(6592+INT(PX/8)+PY*20):HS=INT(PN/16):HE=PN-HS*16:BN$=B$(HS)+B$(HE)
341 M$=MID$(BN$,PX-INT(PX/8)*8+1,1)
342 RETURN
350 LOCATE 1,5:BOX 26,8,134,16:PRINT "退出吗?(Y/N)";
351 A$=INKEY$:IF A$="y" THEN CLEAR:END
352 IF A$="n" THEN LOCATE 1,5:PRINT SPC(12):BOX 26,8,134,16,0,0:65
353 GOTO 351
360 LOCATE 1,1:PRINT "SF:";SFK;" CS:";CK;
361 PRINT " DB:";DK;" WL:";WK;
365 INKEY$:LOCATE 1,1:PRINT SPC(19)
366 GOTO 69
390 TX=EX:TY=EY:DI=SGN(WX-EX):GOTO 392
391 TX=WX:TY=WY:DI=DIR
392 LINE TX-2,TY+1,TX-2,TY+2:LINE TX,TY-2,TX,TY+2:LINE TX+2,TY+1,TX+2,TY+2
393 LINE TX-1,TY,TX+1,TY:LINE TX,TY-1,TX+2*DI,TY-1
394 RETURN
400 LOCATE 1,6:PRINT S$:FOR I=1  TO 250:NEXT I:LOCATE 1,6:PRINT SPC(9):RETURN
410 GOSUB 337:IF M$="0" THEN CIRCLE WX,WY,2,1,0:WY=WY+1:410
411 IF WY>51 THEN LOCATE 1,1:PRINT "掉下去了!挂了!":INKEY$:11 
412 RETURN
420 GOSUB 336:IF M$="0" THEN EY=EY+1:420
421 IF EY>51 THEN LOCATE 1,1:PRINT "YEAH!把它打下去了!":EBL=0:200
422 RETURN
430 IF KY<53 AND KOY>2 AND KY>2 THEN CIRCLE KOX,KOY,2,1,0:CIRCLE KX,KY,2,1
432 IF KBY>2 AND KDY>2 AND BZ=0 THEN CIRCLE KOX,KDY,2,1,0:CIRCLE KX,KBY,2,1
434 IF KBY<3 THEN BOX 1,0,159,10,1,0
435 KVY=KVY+1:KVX=KVX+WD*(2-KB)
437 KOX=KX:KOY=KY:KDY=KBY:KX=KX+KVX:KY=KY+KVY:IF BZ=0 THEN KBY=KBY+KVY+1
438 IF KY>53 THEN ON KB GOTO 200,321
440 IF KBY>53 THEN CIRCLE KOX,KDY,2,1,0:BZ=1
450 IF KX>158 OR KX<3 THEN 197
451 GOSUB 339
454 IF M$="1" AND BZ=0 AND CS=1 THEN CIRCLE WX,WY,3,1,0:CS=0:WX=KX:WY=KDY:197
455 IF M$="1" AND BZ=0 THEN BZ=1:GOSUB 496:ON KB GOTO 182,324
458 IF M$="1" THEN GOSUB 499:ON KB GOTO 180,320
460 GOTO 430
496 BZX=KX:BZY=KBY:GOTO 500
497 BZX=BX:BZY=BY:GOTO 500
499 BZX=KX:BZY=KY
500 FOR I=1  TO 6:CIRCLE BZX,BZY,I,1:NEXT I:FOR I=1  TO 6:CIRCLE BZX,BZY,I,1,0:NEXT I
501 RETURN
510 OPEN M$ FOR INPUT AS#1
515 GRAPH:INPUT #1,XN,XM,YM:INPUT #1,BIF:IF BIF=0 THEN 535
520 INPUT #1,BNU
525 FOR I=1  TO BNU
530 INPUT #1,RX,RY,RXX,RYY:BOX RX,RY,RXX,RYY,1:NEXT I
535 INPUT #1,EIF:IF EIF=0 THEN 555
540 INPUT #1,EIN
545 FOR I=1  TO EIN
550 INPUT #1,RX,RY,RXX,RYY:ELLIPSE RX,RY,RXX,RYY,1:NEXT I
555 INPUT #1,ECIF:IF ECIF=0 THEN 575
560 INPUT #1,ECN
565 FOR I=1  TO ECN
570 INPUT #1,RX,RY,RXX,RYY:ELLIPSE RX,RY,RXX,RYY,1,0:NEXT I
575 CLOSE #1:BOX 1,60,159,80:ELLIPSE 20,62,17,10,1,0:ELLIPSE 20,62,17,10
580 WX=INT(RND(1)*XM+XN):WY=YM
585 GOTO 60
600 FOR I=1  TO 60:NEXT I:BOX 40,69,40+POW*2,70:POW=POW+0.5:IF PEEK(205)=255 THEN 109
601 IF POW>14 THEN POW=14:109 
602 GOTO 600
650 CLS:BOX 1,10,159,80:LOCATE 1,2:PRINT "选择战役地图:"
653 LOCATE 2,2:PRINT "地图名:";CHR$(17);SPC(8);CHR$(16)
655 LOCATE 3,2:PRINT "介绍:"
660 OPEN "MLST" FOR INPUT AS#1
661 INPUT #1,BNU
662 FOR I=1  TO BNU
663 INPUT #1,M$(I),MJ$(I),MF$(I)
664 NEXT I:CLOSE #1
665 LOCATE 2,10:PRINT SPC(8):LOCATE 2,10:PRINT M$(K)
666 LOCATE 3,7:PRINT SPC(34):LOCATE 3,7:PRINT MJ$(K)
670 KV=ASC(INKEY$):IF KV=22 THEN K=K+1
675 IF KV=23 THEN K=K-1
680 IF K>BNU OR K<1 THEN K=ABS(K-BNU)
685 IF KV=13 THEN M$=MF$(K):K=1:CLS:LOCATE 2,7:PRINT "Z    Y":47
690 GOTO 665