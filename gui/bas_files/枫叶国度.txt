0 :
1 DIM MA$(15),NR$(15):TQ$(1)="":TQ$(2)="":TQ$(3)="":TQ$(4)=""
2 DIM D(18),O(18),J(18)
3 DIM H$(26)
5 REM 枫叶国度V1.5完整版
6 GRAPH
7 LOCATE 3,6 :PRINT "Loading..."
8 OPEN "MA-MAP" FOR INPUT AS#1
9 FOR I=1  TO 14 :INPUT #1,MA$(I),NR$(I):NEXT
10 CLOSE #1
15 OPEN "MA-DATA" FOR INPUT AS#1
16 FOR I=1  TO 6 :INPUT #1,PL$(I),YZ$(I),CZ(I),JG(I),MC(I),DS(I):NEXT
17 FOR I=1  TO 6 :INPUT #1,DW$(I),DY$(I),ZC(I),MJ(I),YL(I),YQ(I):NEXT
18 DIM TB$(22)
19 FOR I=1  TO 22 :INPUT #1,TB$(I):NEXT
20 FOR I=1  TO 5 :INPUT #1,ZB$(I),BY$(I),DJ(I):NEXT
21 FOR I=1  TO 5 :INPUT #1,KS$(I),KY$(I),CP(I):NEXT
25 CLOSE #1
30 OPEN "MA-TALK" FOR INPUT AS#1
31 FOR I=1  TO 26 :INPUT #1,H$(I):NEXT:CLOSE #1
50 GRAPH
51 BOX 5,5,20,20,1,1 :BOX 8,8,20,20,1,0 :BOX 154,75,139,60,1,1 :BOX 151,72,139,60,1,0
52 BOX 154,5,139,20,1,1 :BOX 151,8,136,20,1,0 :BOX 5,75,20,60,1,1
53 BOX 8,72,20,60,1,0
54 LOCATE 1,5 :PRINT "喾阋豆度"
55 LOCATE 2,2 :PRINT "⑴启程":LOCATE 3,8 :PRINT "⑵往事":LOCATE 4,14 
56 PRINT "⑶离开"
57 LOCATE 5,4 :PRINT "Ver1.5  完整版";
58 LOCATE 2,9 :PRINT "MapleStory":LOCATE 4,3 :PRINT "Edjason~"
59 A=ASC(INKEY$)
60 IF A=98 THEN 150
61 IF A=110 THEN 100
62 IF A=109 THEN 70
63 GOTO 59
70 CLS
71 PRINT "枫の谷工作组"
72 PRINT "Edjason倾情制作"
73 A$=INKEY$
74 PRINT "E-mail:edjason1993@yahoo.com.cn";
75 END
100 CLS:LOCATE 3,6 :PRINT "Loading..."
101 OPEN "MA-SAVE" FOR INPUT AS#1
102 INPUT #1,NA$,TX$,HP,MO,GZ,S,F,DAY,XQ,TQ,ZB,LV
103 FOR I=1  TO 6 :INPUT #1,DW(I):NEXT:FOR I=1  TO 6
104 INPUT #1,SE(I):NEXT:FOR I=1  TO 5 :INPUT #1,K(I):NEXT
105 INPUT #1,TA,TB,RA,RB,RC,RW1,RW2
106 FOR I=1  TO 18 :INPUT #1,D(I),O(I),J(I):NEXT
107 INPUT #1,X,Y,P
108 FOR I=1  TO 6 :INPUT #1,WT(I),TR(I):NEXT:INPUT #1,DYB
110 CLOSE #1:GOTO 200
150 TEXT
151 INPUT "姓名：";NA$
155 GRAPH:PRINT "创建ing..."
156 TX$="":S=7:F=0:DAY=1:XQ=1:TQ=1:HP=100:ZB=1:LV=0:RW1=0:RW2=1
157 FOR I=1  TO 6 :DW(I)=0 :NEXT
158 FOR I=1  TO 6 :SE(I)=0 :NEXT
159 TA=0:TB=0:MO=100:GZ=0
160 FOR I=1  TO 5 :K(I)=0:NEXT
161 RA=0:RB=0:RC=0
162 FOR I=1  TO 18 :D(I)=0:O(I)=0:J(I)=0 :NEXT
163 X=3:Y=5:P=3
190 CLS:PRINT "OK!":A$=INKEY$
200 GRAPH
201 PRINT MID$(MA$(P),1,16)
202 PRINT MID$(MA$(P),17,16)
203 PRINT MID$(MA$(P),33,16)
204 PRINT MID$(MA$(P),49,16)
205 PRINT MID$(MA$(P),65,16);
206 LOCATE 1,17 :PRINT S :LOCATE 1,19 :PRINT "时"
207 LOCATE 2,17 :PRINT F :LOCATE 2,19 :PRINT "分"
208 LOCATE 3,18 :PRINT TQ$(TQ)
209 LOCATE 4,17 :PRINT "星期":LOCATE 5,18 :PRINT XQ;
210 LOCATE X,Y :PRINT TX$;
215 IF PEEK(199)>128 THEN JA=ASC(INKEY$)
216 IF JA=20 AND P=3 AND X=3 AND Y=5 THEN P=13:X=5:Y=9:JA=0:200
217 IF JA=20 AND P=6 AND X=2 AND (Y=7 OR Y=9) THEN P=10:X=5:JA=0:200
218 IF JA=21 AND P=7 AND X=5 THEN P=2:X=4:Y=5:JA=0:200
219 IF JA=21 AND P=10 AND X=5 THEN P=6:X=2:JA=0:200
220 IF JA=21 AND P=13 AND X=5 THEN P=3:X=3:Y=5:JA=0:200
225 IF JA=21 AND X=5 THEN X=1:P=P-1:JA=0:200
226 IF JA=20 AND X=1 THEN X=5:P=P+1:JA=0:200
227 IF JA=23 AND Y=1 THEN Y=15:P=P-2:JA=0:200
228 IF JA=22 AND Y=15 THEN Y=1:P=P+2:JA=0:200
230 IF JA=20 AND X>1 THEN QQ=-1
231 IF JA=21 AND X<5 THEN QQ=1
232 IF JA=22 AND Y<15 THEN AA=2
233 IF JA=23 AND Y>1 THEN AA=-2
234 SE=SE+1
235 IF SE>=150 THEN SE=0:F=F+1
236 IF F=60 THEN F=0:S=S+1 :LOCATE 2,18 :PRINT " "
237 IF S>=18 THEN 805
238 IF JA=13 THEN JA=0:300
239 IF JA=28 THEN JA=0:400
240 IF CHR$(JA)="w" AND (P=7 OR P=8 OR P=9) THEN JA=0:386
241 IF DAY=20 THEN 3000
242 IF DAY=50 THEN 3500
243 IF DAY=123 THEN 4000
250 IF QQ=0 AND AA=0 THEN JA=0:206
251 X0=X+QQ:Y0=Y+AA
252 K=ASC(MID$(NR$(P),(X0-1)*8+(Y0+1)/2,1))
253 IF K=1 THEN 254 ELSE 255
254 LOCATE X,Y :PRINT "　";:X=X0:Y=Y0
255 JA=0:QQ=0:AA=0:X0=0:Y0=0:GOTO 206
300 IF X<>1 THEN EE=ASC(MID$(NR$(P),(X-2)*8+(Y+1)/2,1))
301 IF X<>5 THEN DD=ASC(MID$(NR$(P),X*8+(Y+1)/2,1))
302 IF Y<>1 THEN SS=ASC(MID$(NR$(P),(X-1)*8+(Y+1)/2-1,1))
303 IF Y<>15 THEN FF=ASC(MID$(NR$(P),(X-1)*8+(Y+1)/2+1,1))
305 IF EE=3 OR DD=3 OR SS=3 OR FF=3 THEN 330
306 IF EE=4 OR DD=4 OR SS=4 OR FF=4 THEN 335
307 IF EE=8 OR DD=8 OR SS=8 OR FF=8 THEN 500
308 IF EE=9 OR DD=9 OR SS=9 OR FF=9 THEN 1000
309 IF EE=11 OR DD=11 OR SS=11 OR FF=11 THEN 550
310 IF EE=12 OR DD=12 OR SS=12 OR FF=12 THEN 355
311 IF EE=13 OR DD=13 OR SS=13 OR FF=13 THEN 350
312 IF EE=15 OR DD=15 OR SS=15 OR FF=15 THEN 600
313 IF EE=16 OR DD=16 OR SS=16 OR FF=16 THEN P=7:X=5:Y=7:200
314 IF EE=17 OR DD=17 OR SS=17 OR FF=17 THEN 700
315 IF EE=19 OR DD=19 OR SS=19 OR FF=19 THEN 800
316 IF EE=20 OR DD=20 OR SS=20 OR FF=20 THEN 360
317 IF EE=21 OR DD=21 OR SS=21 OR FF=21 THEN 380
320 GOTO 206
330 IF RA=0 AND P=1 THEN RA=1:333
331 IF RB=0 AND P=2 THEN RB=1:333
332 GOTO 206
333 CLS:PRINT "得到[红花草]":TA=TA+1
334 A$=INKEY$:GOTO 200
335 CLS:LOCATE 3,2 :PRINT "平静的湖水如同镜面":BOX 0,0,160,80,1,2
336 A=ASC(INKEY$)
337 IF A=101 THEN 9000 ELSE 6000
350 IF RC=0 THEN 351 ELSE 206
351 RC=1:CLS:PRINT "得到[蜂蜜]":TB=TB+1:A$=INKEY$:GOTO 200
355 CLS:IF GZ=1 THEN PRINT H$(19):A$=INKEY$:GOTO 200 ELSE PRINT H$(16):A$=INKEY$
356 CLS:IF TB>=20 THEN PRINT H$(18):TB=TB-20:GZ=1 ELSE PRINT H$(17)
357 A$=INKEY$:GOTO 200 
360 A=INT(RND(1)*5)+9:IF A=10 THEN A=11
361 CLS:PRINT H$(A):A$=INKEY$:
362 IF DAY=123 THEN CLS:PRINT H$(10):A$=INKEY$
372 GOTO 200
380 FOR I=1  TO 8 :CLS:PRINT H$(I):A$=INKEY$:NEXT
381 GOTO 200
386 CLS:IF HP<10 THEN PRINT "你已经体力透支别挖了":A$=INKEY$:GOTO 200
387 A=INT(RND(1)*200)+1
388 W=1
389 IF A<20 THEN W=2
390 IF A>20 AND A<20+(DJ(ZB)+LV)*2 THEN W=3
391 IF A>80 AND A>(DJ(ZB)+LV)+80 THEN W=4
392 IF A=199 AND ZB=4 THEN W=5
393 IF W=0 THEN PRINT "什么也没挖到!" ELSE K(W)=K(W)+1:PRINT "挖到";KS$(W);KY$(W)
394 HP=HP-10:A$=INKEY$:GOTO 200
400 CLS
401 PRINT "ツ柯疾说"
402 PRINT "⑴日志管理⑵物品查看"
403 PRINT "⑶祭祀活动⑷瞬移之术","⑸购买彩票⑹系统菜单"
404 PRINT "MapleStory枫叶国度";
405 A=ASC(INKEY$)
406 IF A=98 THEN 415
407 IF A=110 THEN 430
408 IF A=109 THEN 455
409 IF A=103 THEN P=3:X=3:Y=5:200
410 IF A=104 THEN 485
411 IF A=106 THEN 465
412 IF A=27 THEN 200 
413 GOTO 405
415 CLS:PRINT "ジ鋈巳罩绝"
416 PRINT "神木村第";DAY;"天","现在";S;"点";F;"分"
417 PRINT "记录者：";TX$;NA$,"天气状况：";TQ$(TQ);
418 A$=INKEY$:CLS
419 PRINT "金钱：";MO,"体力：";HP;"/100","种植面积：";
420 FOR I=1  TO 18 :MJ=MJ+(D(I)>0):NEXT
421 PRINT MJ;"亩","钓鱼技术：";DYB
422 A$=INKEY$:MJ=0:GOTO 400
430 CLS:PRINT "物品管理："
431 PRINT YZ$(1);PL$(1);"×";SE(1)
432 PRINT YZ$(2);PL$(2);"×";SE(2)
433 PRINT YZ$(3);PL$(3);"×";SE(3)
434 PRINT YZ$(4);PL$(4);"×";SE(4);
435 A$=INKEY$:CLS
436 PRINT YZ$(5);PL$(5);"×";SE(5)
437 PRINT YZ$(6);PL$(6);"×";SE(6)
438 PRINT DW$(1);DY$(1);"×";DW(1)
439 PRINT DW$(2);DY$(2);"×";DW(2)
440 PRINT DW$(3);DY$(3);"×";DW(3);
441 A$=INKEY$:CLS:PRINT DW$(4);DY$(4);"×";DW(4)
442 PRINT DW$(5);DY$(5);"×";DW(5)
443 PRINT DW$(6);DY$(6);"×";DW(6)
444 PRINT KS$(1);KY$(1);"×";K(1)
445 PRINT KS$(2);KY$(2);"×";K(2);:A$=INKEY$:CLS
446 PRINT KS$(3);KY$(3);"×";K(3)
447 PRINT KS$(4);KY$(4);"×";K(4)
448 PRINT KS$(5);KY$(5);"×";K(5)
449 PRINT "当前铁锹：";ZB$(ZB);BY$(ZB),"等级：";LV+DJ(ZB);
450 A$=INKEY$:CLS:PRINT "蜂蜜：";TB,"红花草：";TA :A$=INKEY$:GOTO 400
455 CLS:IF DAY>123 THEN PRINT "祭祀活动已经结束!":A$=INKEY$:GOTO 200
456 PRINT "离祭祀活动还有";123-DAY;"天","祭祀物品100个红花草"
457 A$=INKEY$:GOTO 400
465 CLS:PRINT "ハ低巢说"
466 PRINT "1.读取存档","2.写入存档","3.退出游戏"
467 A=ASC(INKEY$)
468 IF A=98 THEN 100
469 IF A=110 THEN 473
470 IF A=109 THEN 70
471 IF A=27 THEN 400
472 GOTO 467
473 CLS:LOCATE 3,6 :PRINT "Saving...."
474 OPEN "MA-SAVE" FOR OUTPUT AS#1
475 WRITE #1,NA$,TX$,HP,MO,GZ,S,F,DAY,XQ,TQ,ZB,LV
476 FOR I=1  TO 6 :WRITE #1,DW(I):NEXT:FOR I=1  TO 6
477 WRITE #1,SE(I):NEXT:FOR I=1  TO 5 :WRITE #1,K(I):NEXT
478 WRITE #1,TA,TB,RA,RB,RC,RW1,RW2
479 FOR I=1  TO 18 :WRITE #1,D(I),O(I),J(I):NEXT
480 WRITE #1,X,Y,P
481 FOR I=1  TO 6 :WRITE #1,WT(I),TR(I):NEXT:WRITE #1,DYB :CLOSE #1 
482 A$=INKEY$:GOTO 400
485 CLS:PRINT "10元一张彩票!","(Y/N)"
486 A=ASC(INKEY$)
487 IF A=121 THEN 488 ELSE 400
488 CLS:IF MO<10 THEN PRINT "钱不够^":A$=INKEY$:GOTO 400
489 A=INT(RND(1)*200)+1:MO=MO-10
490 IF A=123 THEN PRINT "中了500元":MO=MO+100:495
491 IF A=111 OR (A>50 AND A<60) THEN PRINT "中了50元!":MO=MO+50:495
492 IF A=151 THEN PRINT "中了1000元!":MO=MO+1000:495
493 PRINT "没中奖……"
495 A$=INKEY$:GOTO 400
500 CLS:PRINT "嗷队光临种子店","1.购买","2.退出"
501 A=ASC(INKEY$)
502 IF A=98 THEN 505
503 IF A=110 THEN 200
504 GOTO 501
505 CLS:PRINT "1.";PL$(1);YZ$(1);JG(1)
506 PRINT "2.";PL$(2);YZ$(2);JG(2)
507 PRINT "3.";PL$(3);YZ$(3);JG(3)
508 PRINT "4.";PL$(4);YZ$(4);JG(4)
509 PRINT "5.";PL$(5);YZ$(5);JG(5);
510 A=ASC(INKEY$)
511 IF A=98 THEN U=1:520
512 IF A=110 THEN U=2:520
513 IF A=109 THEN U=3:520
514 IF A=103 THEN U=4:520
515 IF A=104 THEN U=5:520
516 IF A=27 THEN 200
517 GOTO 510
520 CLS:PRINT "你要多少个?","要";M;"","需钱";M*JG(U);"金币"
521 A=ASC(INKEY$)
522 IF A=20 AND M<99 THEN M=M+1:GOTO 520
523 IF A=21 AND M>0 THEN M=M-1:GOTO 520
524 IF A=20 AND M=99 THEN M=0:GOTO 520
525 IF A=21 AND M=0 THEN M=99:GOTO 520
526 IF A=13 THEN 530
527 IF A=27 THEN 200
528 GOTO 521
530 IF MO<JG(U)*M THEN CLS:PRINT "没钱啊!":A$=INKEY$:GOTO 200
531 MO=MO-JG(U)*M:SE(U)=SE(U)+M:CLS:PRINT "购买成功!":A$=INKEY$:GOTO 500
550 CLS:PRINT "甓物交易中心"
551 PRINT "1.";DW$(1);MJ(1);" 2.";DW$(2);MJ(2),"3.";DW$(3);MJ(3);" 4.";DW$(4);MJ(4)
552 PRINT "5.";DW$(5);MJ(5);" 6.";DW$(6);MJ(6);
553 A=ASC(INKEY$)
554 IF A=98 THEN U=1:563
555 IF A=110 THEN U=2:563
556 IF A=109 THEN U=3:563
557 IF A=103 THEN U=4:563
558 IF A=104 THEN U=5:563
559 IF A=106 THEN U=6:563
560 IF A=27 THEN 200
561 GOTO 553
563 CLS
564 IF DW(U)<>0 THEN PRINT "你已经有了!":A$=INKEY$:GOTO 200 
565 IF MO<MJ(U) THEN PRINT "没钱啊……":A$=INKEY$:GOTO 200
566 MO=MO-MJ(U):DW(U)=DW(U)+1:PRINT "交易成功!":A$=INKEY$:GOTO 200
600 CLS:PRINT "苷饫锸翘匠铺"
601 PRINT "1.冶炼器具","2.提升等级"
602 A=ASC(INKEY$)
603 IF A=98 THEN 2000
604 IF A=110 THEN 607
605 IF A=27 THEN 200
606 GOTO 602
607 CLS:PRINT "提升一个等级需";(LV+1)*10*ZB;"金币","(Y/N)"
608 A=ASC(INKEY$)
609 IF A=121 THEN 612
610 IF A=110 THEN 200
611 GOTO 608
612 CLS:IF MO<(LV+1)*10*ZB THEN PRINT "没钱啊……":A$=INKEY$:GOTO 200
613 MO=MO-(LV+1)*10*ZB:LV=LV+1
614 PRINT "升级成功!":A$=INKEY$:GOTO 200
700 CLS:PRINT H$(14):IF DAY=10 AND DW(4)=0 THEN 701 ELSE 702
701 A$=INKEY$:CLS:PRINT H$(15):DW(4)=1
702 A$=INKEY$:GOTO 200
800 CLS:PRINT "你想睡觉吗?(Y/N)"
801 A=ASC(INKEY$)
802 IF A=121 AND S>15 THEN 805 ELSE IF A=121 AND S<=15 THEN 5500
803 IF A=110 THEN 200
804 GOTO 801
805 CLS:PRINT "睡觉前把今天的收成卖掉点吧!":Y=0
806 A$=INKEY$
807 CLS
808 PRINT "正在整理清单..."
809 FOR I=1  TO 2000 :NEXT:CLS:M=0
810 CLS:PRINT "农作物：","1.";PL$(1);"2.";PL$(2),"3.";PL$(3);"4.";PL$(4),"5.";PL$(5);
811 PRINT "6.";PL$(6),"[S]翻页";
812 A=ASC(INKEY$)
813 IF A=98 THEN U=1:820 ELSE IF A=110 THEN U=2:820 ELSE IF A=109 THEN U=3:820
814 IF A=103 THEN U=4:820 ELSE IF A=104 THEN U=5:820 ELSE IF A=106 THEN U=6:820
815 IF CHR$(A)="s" THEN 830
816 GOTO 812
820 CLS:IF SE(U)=0 THEN PRINT "没有东西!":A$=INKEY$:GOTO 810
821 CLS:PRINT "有";SE(U);"个","拿出";M;"","卖价:";MC(U)*M;"元"
822 A=ASC(INKEY$)
823 IF A=20 AND M<SE(U) THEN M=M+1:821
824 IF A=21 AND M>0 THEN M=M-1:821
825 IF A=13 THEN 828
826 IF A=27 THEN 810
827 GOTO 822
828 CLS:PRINT "已卖出!":Y=Y+MC(U)*M:SE(U)=SE(U)-M:A$=INKEY$:M=0:GOTO 810
830 CLS:PRINT "动物牲畜："
831 PRINT "1.";DW$(1);"2.";DW$(2),"3.";DW$(3);"4.";DW$(4),"5.";DW$(5);"6.";DW$(6)
832 PRINT "[A]上页  [S]翻页";
833 A=ASC(INKEY$)
834 IF A=98 THEN U=1:840 ELSE IF A=110 THEN U=2:840 ELSE IF A=109 THEN U=3:840
835 IF A=103 THEN U=4:840 ELSE IF A=104 THEN U=5:840 ELSE IF A=106 THEN U=6:840
836 IF A=97 THEN 810 ELSE IF A=115 THEN 860
837 GOTO 833
840 CLS:IF DW(U)=0 THEN PRINT "你没有啊!":A$=INKEY$:GOTO 830
841 PRINT "你真的要卖掉";DW$(U);"吗?(Y/N)"
842 A=ASC(INKEY$)
843 IF A=121 THEN 846
844 IF A=110 THEN 830
845 GOTO 842
846 CLS:PRINT "已卖出!":DW(U)=0:Y=Y+MJ(U)*80/100+TR(U)*5:TR(U)=0:WT(U)=0
847 A$=INKEY$:GOTO 830
860 CLS:PRINT "矿石栏："
861 PRINT "1.";KS$(1);"2.";KS$(2),"3.";KS$(3);"4.";KS$(4),"5.";KS$(5)
862 PRINT "[A]上页 [S]翻页";
863 A=ASC(INKEY$)
864 IF A=98 THEN U=1:870 ELSE IF A=110 THEN U=2:870 ELSE IF A=109 THEN U=3:870
865 IF A=103 THEN U=4:870 ELSE IF A=104 THEN U=5:870 ELSE IF A=97 THEN 830
866 IF A=115 THEN 890
867 GOTO 863
870 CLS:IF K(U)=0 THEN PRINT "没有东西!":A$=INKEY$:GOTO 860
871 CLS:PRINT "有";K(U);"个","拿出";M;"","卖价:";CP(U)*M;"元"
872 A=ASC(INKEY$)
873 IF A=20 AND M<K(U) THEN M=M+1:871
874 IF A=21 AND M>0 THEN M=M-1:871
875 IF A=13 THEN 878
876 IF A=27 THEN 860
877 GOTO 872
878 CLS:PRINT "已卖出!":K(U)=K(U)-M:Y=Y+CP(U)*M:A$=INKEY$:M=0:GOTO 860
890 CLS:PRINT "其它栏：","1.红花草","2.蜂蜜","[A]上页  [输入]结账"
891 A=ASC(INKEY$)
892 IF A=98 THEN 895 ELSE IF A=110 THEN 905
893 IF A=97 THEN 860 ELSE IF A=13 THEN 920
894 GOTO 891
895 CLS:IF TA=0 THEN PRINT "没有啊……":A$=INKEY$:GOTO 890
896 CLS:PRINT "有";TA;"个","卖出";M;"","共";M*10;"元"
897 A=ASC(INKEY$)
898 IF A=20 AND M<TA THEN M=M+1:896
899 IF A=21 AND M>0 THEN M=M-1:896
900 IF A=13 THEN 903
901 IF A=27 THEN 890
902 GOTO 897
903 CLS:PRINT "已卖出!":TA=TA-M:Y=Y+M*10:A$=INKEY$:M=0:GOTO 890
905 IF TB<=0 THEN PRINT "你没有……":A$=INKEY$:GOTO 890
906 CLS:PRINT "有";TB;"个","卖出";M;"","共";M*15;"元"
907 A=ASC(INKEY$)
908 IF A=20 AND M<TB THEN M=M+1:906
909 IF A=21 AND M>0 THEN M=M-1:906
910 IF A=13 THEN 913
911 IF A=27 THEN 890
912 GOTO 907
913 CLS:PRINT "已卖出!":TB=TB-M:Y=Y+M*15:A$=INKEY$:M=0:GOTO 890
920 CLS:PRINT "今天一共赚了";Y;"元"
921 IF Y=0 THEN PRINT "扣去10％金币!":MO=MO*90/100
922 MO=MO+Y
923 Y=0 :A$=INKEY$:CLS
924 PRINT "睡觉ing..."
925 FOR I=1  TO 2000 :NEXT
926 HP=100:RA=0:RB=0:RC=0:DAY=DAY+1:S=6:F=0:TQ=INT(RND(1)*4)+1
927 FOR I=1  TO 18 :O(I)=O(I)+1+(TQ=3):J(I)=0:NEXT
928 FOR I=1  TO 6 :WT(I)=WT(I)+(DW(I)=1)*1:NEXT
929 XQ=XQ+1:IF XQ=8 THEN XQ=1
930 IF GZ=1 AND TQ<>3 THEN FOR I=1  TO 18 :O(I)=O(I)+1:NEXT
940 X=3:Y=5:P=3:CLS:PRINT "新的一天开始了!":A$=INKEY$:GOTO 200
1000 CLS:GOTO 1700
1001 CLS:PRINT MID$(MA$(14),1,16)
1002 PRINT MID$(MA$(14),17,16)
1003 PRINT MID$(MA$(14),33,16)
1004 PRINT MID$(MA$(14),49,16)
1005 PRINT MID$(MA$(14),65,16);
1006 LOCATE X,Y :PRINT TX$;
1007 LOCATE 1,17 :PRINT S :LOCATE 1,19 :PRINT "时":LOCATE 2,17 :PRINT F
1008 LOCATE 2,19 :PRINT "分":LOCATE 3,18 :PRINT TQ$(TQ)
1009 LOCATE 4,17 :PRINT "星期":LOCATE 5,18 :PRINT XQ;
1010 IF PEEK(199)>128 THEN A=ASC(INKEY$)
1011 IF A=21 AND X<4 THEN LOCATE X,Y :PRINT "":X=X+1:TT=1:1030
1012 IF A=20 AND X>2 AND X<>5 THEN LOCATE X,Y :PRINT "":X=X-1:TT=1:1030
1013 IF A=22 AND Y<13 AND X<>5 THEN LOCATE X,Y :PRINT "";:Y=Y+2:TT=1:1030
1014 IF A=20 AND X=5 THEN LOCATE X,Y :PRINT "　";:X=4:TT=1:1030
1015 IF A=23 AND X=5 AND Y=9 THEN LOCATE X,Y :PRINT "　";:Y=7:TT=1:1030
1016 IF A=22 AND X=5 AND Y=7 THEN LOCATE X,Y :PRINT "　";:Y=9:TT=1:1030
1017 IF A=21 AND X=5 THEN P=3:X=3:Y=7:200
1018 IF A=122 AND X<>5 THEN 1500
1019 IF A=120 AND X<>5 THEN A=0:1300
1020 IF A=99 AND X<>5 THEN A=0:1100
1021 IF A=28 THEN Q=1:400
1025 IF A=23 AND Y>3 AND X<>5 THEN LOCATE X,Y :PRINT "";:Y=Y-2:TT=1:1030
1026 IF A=21 AND X=4 AND (Y=7 OR Y=9) THEN LOCATE X,Y :PRINT "";:X=5:TT=1:1030
1030 SE=SE+1
1031 IF SE>=10 THEN SE=0:F=F+1
1032 IF F>=60 THEN F=F-60:S=S+1
1033 IF S=18 THEN 805
1034 IF TT=1 THEN A=0:1006 ELSE A=0:1010
1100 CLS
1101 E=(X-2)*6+(Y+1)/2-1
1150 CLS:IF D(E)=0 THEN PRINT "此地没种植任何作物!":A$=INKEY$:GOTO 1001
1151 PRINT "农作物：";PL$(D(E)),"成长度：";
1152 IF O(E)>CZ(D(E)) THEN O(E)=CZ(D(E))
1153 PRINT O(E);"/";CZ(D(E))
1154 A$=INKEY$:GOTO 1001
1300 :
1301 E=(X-2)*6+(Y+1)/2-1
1302 IF D(E)=0 THEN CLS:PRINT "没农作物浇什么水?":A$=INKEY$:GOTO 1001
1303 IF GZ<>0 THEN CLS:PRINT "已经有矮人帮你工作!":A$=INKEY$:GOTO 1001
1304 IF TQ=3 THEN CLS:PRINT "今天是雨天，不用浇水":A$=INKEY$:GOTO 1001
1305 IF O(E)>=CZ(D(E)) THEN CLS:PRINT "已经成熟了……":A$=INKEY$:GOTO 1001
1306 IF HP<2 THEN CLS:PRINT "你已经精疲力竭了……":A$=INKEY$:GOTO 1001
1307 IF J(E)=1 THEN CLS:PRINT "已经浇过水了!":A$=INKEY$:GOTO 1001 
1350 FOR I=1  TO 5 :LOCATE X,Y :PRINT "":FOR R=1  TO 1000 :NEXT:LOCATE X,Y :PRINT ""
1351 NEXT
1353 O(E)=O(E)+1:F=F+10:HP=HP-2:J(E)=1
1354 GOTO 1001
1500 CLS:E=(X-2)*6+(Y+1)/2-1
1501 PRINT "1.种植","2.采摘"
1502 A=ASC(INKEY$)
1503 IF A=98 THEN 1510 
1504 IF A=110 THEN 1600
1505 IF A=27 THEN 1001
1506 GOTO 1502
1510 CLS:IF HP<2 THEN PRINT "没力气就别种了……":A$=INKEY$:GOTO 1001
1511 IF D(E)<>0 THEN PRINT "此地已种植其它作物!"
1520 CLS
1521 PRINT "你要种什么？"
1522 PRINT "1.";PL$(1);"2.";PL$(2),"3.";PL$(3);"4.";PL$(4),"5.";PL$(5);"6.";PL$(6)
1523 A=ASC(INKEY$)
1524 IF A=98 THEN U=1:1550 ELSE IF A=110 THEN U=2:1550 ELSE IF A=109 THEN U=3:1550
1525 IF A=103 THEN U=4:1550 ELSE IF A=104 THEN U=5:1550 ELSE IF A=106 THEN U=6:1550
1526 IF A=27 THEN 1001
1527 GOTO 1523
1550 CLS:IF SE(U)=0 THEN PRINT "没有啊……":A$=INKEY$:GOTO 1001
1555 CLS:PRINT "种植中……"
1556 D(E)=U:O(E)=0:SE(U)=SE(U)-1:F=F+10
1557 HP=HP-2
1558 A$=INKEY$:GOTO 1001
1600 CLS:E=(X-2)*6+(Y+1)/2-1
1601 IF D(E)=0 THEN PRINT "没种东西怎么摘?":A$=INKEY$:GOTO 1001
1602 IF O(E)<CZ(D(E)) THEN PRINT "还未成熟……":A$=INKEY$:GOTO 1001
1603 IF HP<3 THEN PRINT "体力不够!":A$=INKEY$:GOTO 1001
1605 CLS:PRINT "采摘ing..."
1606 PRINT "得到";DS(D(E));"个"
1607 SE(D(E))=SE(D(E))+DS(D(E)):HP=HP-3:D(E)=0:O(E)=0
1608 A$=INKEY$:GOTO 1001
1700 CLS
1701 PRINT "你要去哪儿?","1.农田","2.农场"
1702 A=ASC(INKEY$)
1703 IF A=98 THEN 1001
1704 IF A=110 THEN 1800
1705 IF A=27 THEN X=3:Y=7:P=3:200
1706 GOTO 1702
1800 L=1
1850 CLS
1860 PRINT DW$(L);DY$(L)
1865 IF DW(L)=0 THEN PRINT "暂时没有":1890
1868 PRINT "成长：";WT(L),"训练：";TR(L),"[T]训练 [S]翻页"
1869 A=ASC(INKEY$)
1870 IF A=116 THEN 1900
1871 IF A=115 AND L<6 THEN L=L+1 :1850
1872 IF A=115 AND L=6 THEN X=3:Y=7:P=3:200
1873 GOTO 1869
1890 A$=INKEY$:L=L+1:IF L>6 THEN P=3:X=3:Y=7:200
1891 GOTO 1850
1900 CLS:IF HP<3 THEN PRINT "没精力训练……":A$=INKEY$:GOTO 1850
1901 IF MO<10 THEN PRINT "没钱训练!":A$=INKEY$:GOTO 1850
1902 PRINT "训练ing..."
1903 TR(L)=TR(L)+1:MO=MO-10:HP=HP-3
1904 A$=INKEY$:GOTO 1850
2000 CLS
2001 PRINT "让我看看你的铁锹!"
2002 IF ZB=5 THEN PRINT "枫叶锹，看来我没法再做得更好了!":A$=INKEY$:GOTO 200
2003 PRINT "我可以把它冶炼成";ZB$(ZB+1);"但需要10个";KS$(ZB+1)
2004 A$=INKEY$
2005 CLS:IF K(ZB+1)<10 THEN PRINT "你没的话可以去矿井挖":A$=INKEY$:GOTO 200
2006 PRINT "我现在就打造...":ZB=ZB+1:K(ZB)=K(ZB)-10
2007 PRINT "OK!":A$=INKEY$:GOTO 200
3000 LOCATE 3,9 :PRINT "":A$=INKEY$
3001 CLS:PRINT "及⒎桑盒⊙，把钱交出来!"
3002 A$=INKEY$:CLS:PRINT "交钱?(Y/N)"
3003 A=ASC(INKEY$)
3004 IF A=121 THEN CLS:PRINT "及⒎桑赫饣共畈欢!":A$=INKEY$:MO=0:DAY=21:GOTO 200
3005 IF A=110 THEN 3007
3006 GOTO 3003
3007 CLS:PRINT "及⒎桑豪献颖馑滥!":A$=INKEY$
3008 CLS:PRINT "葱晷辏喊⒎扇ニ腊!":A$=INKEY$
3009 CLS:PRINT "";NA$;"：你是……枫の谷的嘘嘘?"
3010 A$=INKEY$:CLS:PRINT "葱晷辏嚎丛谀闳鲜段业姆萆希给你一只鸡!"
3011 A$=INKEY$:DW(1)=1 :TR(1)=30:WT(1)=0:DAY=21:GOTO 200
3500 CLS:PRINT "";NA$;"：啊~~今儿的天真是……"
3501 A$=INKEY$:CLS:PRINT "发现5000金币，是否捡取?(Y/N)"
3502 A=ASC(INKEY$)
3503 IF A=121 THEN 3505 ELSE IF A=110 THEN 3510
3504 GOTO 3502
3505 CLS:PRINT "";NA$;"：自己留着慢慢花~~":A$=INKEY$:MO=MO+5000:RW2=5:DAY=51:GOTO 200
3510 CLS:PRINT "";NA$;"：算了，还是交公吧!":A$=INKEY$:RW2=1:DAY=51:GOTO 200
4000 IF P=12 THEN 4001 ELSE CLS:PRINT "去通天塔!":X=3:Y=13:P=12:A$=INKEY$:GOTO 200
4001 LOCATE 1,1 :PRINT ""
4002 A$=INKEY$:CLS:PRINT "太阳神出来了!"
4003 A$=INKEY$:CLS:IF TA<30 THEN PRINT "没带供品……":A$=INKEY$:DAY=124:200
4004 IF RW2=5 THEN PRINT "来罅ι瘢何抑荒馨阎肿咏桓拾金不昧的人!":A$=INKEY$:DAY=124:200
4005 CLS:PRINT H$(20):A$=INKEY$:PRINT H$(21)
4006 SE(6)=1
4007 A$=INKEY$:DAY=124:GOTO 200
5500 CLS:PRINT "现在太早了","1.在家玩文曲星","2.出去走走"
5501 A=ASC(INKEY$)
5502 IF A=98 THEN 5510 
5503 IF A=110 THEN 200
5504 GOTO 5501
5510 CLS:PRINT "玩玩文曲星","龙之传说、英雄坛说，都不错啊!"
5511 A$=INKEY$
5512 CLS:PRINT "感觉精神多了!":HP=HP+5:S=S+1:A$=INKEY$:GOTO 200
6000 CLS:PRINT "钓鱼比赛","参加(Y/N)":A=ASC(INKEY$)
6001 IF A=121 THEN 6002 ELSE 200
6002 CLS:BA=DYB
6003 LOCATE 1,1 :PRINT "与钓鱼者比拼中……",YLL;":";YPP;" Time:";TIME
6004 A=INT(RND(1)*(250-BA))
6005 IF A<5 THEN YLL=YLL+1
6006 IF (A<15 AND A>10) THEN YPP=YPP+1
6007 TIME=TIME+1
6008 IF TIME=200 THEN 6015
6009 GOTO 6003
6015 CLS:IF YLL>YPP THEN PRINT "You Win","得到1个朱母矿":DYB=DYB+(DYB<150):K(3)=K(3)+1
6016 IF YLL<YPP THEN PRINT "You lose":DYB=DYB-1
6017 IF YLL=YPP THEN PRINT "平手!"
6020 S=S+1:A$=INKEY$:TIME=0:YLL=0:YPP=0:GOTO 200
9000 CLS
9001 PRINT "发现[枫叶石]"
9002 K(5)=K(5)+1
9003 A$=INKEY$:CLS
9004 PRINT "挤悚喂龋耗愫冒!我是Edjason，谢谢你的赏玩!"
9005 A$=INKEY$
9015 GOTO 200