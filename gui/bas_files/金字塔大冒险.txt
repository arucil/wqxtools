0 REM [基本操作]上,下,左,右控制人物移动.输入键停一回合.B键使用炸弹.
1 REM [游戏规则]你走一步,敌人走两步.走出出口为胜.(或使用炸弹)被敌人抓住为失败.
2 REM 失败后扣除HP.为零后死去.过一关可加少量HP.
3 REM 每十关为一卡.可存档一次.共100关.
4 REM [注意事项]过场剧情时请不要狂按.以免错过种要事项.
5 REM 游戏中不要狂按键,会使下一关开使时就乱走一步.
10 :
20 DATA 160,0,169,212,133,64,169,25,133,65,169,192,133,66,169,25,133,67,177,64,145,66,165,64
30 DATA 201,255,208,7,165,65,201,31,208,1,96,230,64,208,2,230,65,230,66,208,2,230,67,76,18,80
40 DATA 162,20,189,235,95,157,235,31,202,208,247,96
50 FOR I=20480 TO 20529:READ A:POKE I,A:NEXT : FOR I=20560 TO 20571:READ A:POKE I,A:NEXT 
60 OPEN "PYRAMID-LOGO.DAT" FOR binary as 1:GRAPH
70 FOR I=704 TO 803:POKE I,0:NEXT : FOR K=1 TO 79
80 fread 1,24556,20:CALL 20560:CALL 20480:sleep 30
90 NEXT K:fread 1,24556,20:CALL 20560:A$=INKEY$
100 CLS:CLEAR:DIM A(36):DIM B(36): GOTO 2560
110 OPEN "PYRAMID-LOGO.DAT" FOR RANDOM AS#1 LEN=80:FIELD #1,80 AS MAP$
120 GET #1,LV+100
130 FOR I=1 TO 6: FOR A=1 TO 6:GC$(I,A)=MID$(MAP$,I*12+A*2-13,2):NEXT A:NEXT I
140 GX=VAL(MID$(MAP$,73,1))
150 GY=VAL(MID$(MAP$,74,1)):LE$="":UP$="":LF$="":H=0:HP=0
160 CX(1)=VAL(MID$(MAP$,76,1))
170 CY(1)=VAL(MID$(MAP$,77,1))
180 CX(2)=VAL(MID$(MAP$,79,1))
190 CY(2)=VAL(MID$(MAP$,80,1))
200 D(1)=VAL(MID$(MAP$,75,1))
210 D(2)=VAL(MID$(MAP$,78,1))
220 CLS:REM 画图
230 FOR I=1 TO 6: FOR A=1 TO 6:B=VAL(GC$(I,A))
240 IF B>7 THEN C=C+10:B=B-8 
250 IF B>3 THEN C=C+100:B=B-4 
260 IF B>1 THEN C=C+1:B=B-2 
270 IF B>0 THEN C=C+1000
280 GC$(I,A)=STR$(C)
290 IF C<1 THEN GC$(I,A)="0000":330
300 IF C<10 THEN GC$(I,A)="0001":330
310 IF C<100 THEN GC$(I,A)="00"+GC$(I,A):330
320 IF C<1000 THEN GC$(I,A)="0"+GC$(I,A):330
330 B=0:C=0:NEXT A:NEXT I
340 FOR I=1 TO 6: FOR A=1 TO 6:LF$=LF$+MID$(GC$(A,I),3,1):NEXT A:NEXT I
350 FOR I=1 TO 36 STEP 6:LE$=LE$+MID$(LF$,I,6)+MID$(GC$(6,(I+5)/6),4,1):NEXT I
360 FOR I=1 TO 6: FOR A=1 TO 6:UP$=UP$+MID$(GC$(A,I),1,1):NEXT A:NEXT I
370 FOR I=1 TO 6:UP$=UP$+MID$(GC$(I,6),2,1):NEXT I
380 BOX 138,10,144,70:BOX 139,70-LIFE,143,70,1,1
390 LINE 138,2,138,8:LINE 140,2,140,8:LINE 138,5,140,5
400 LINE 142,2,142,8:BOX 142,2,144,5
410 LOCATE 2,1:PRINT "*";ZD
420 I=1:HP=INT(I/6+0.9):H=I-(HP*6-6)
430 IF MID$(UP$,I,1)="1" THEN LINE 28+12*H,HP*12-12,40+12*H,HP*12-12
440 IF I<42 THEN 450 ELSE 460 
450 I=I+1:HP=INT(I/6+0.9):H=I-(HP*6-6): GOTO 430
460 I=1:HP=INT(I/7+0.9):H=I-(HP*7-7)
470 IF MID$(LE$,I,1)="1" THEN LINE 28+12*H,HP*12-12,28+12*H,HP*12
480 IF I<42 THEN 490 ELSE 500 
490 I=I+1:HP=INT(I/7+0.9):H=I-(HP*7-7): GOTO 470
500 GOSUB 1440:GOSUB 530:I=1:ON D(1)GOSUB 910,980
510 IF D(2)>0 THEN I=2:ON D(2)GOSUB 910,980
520 GOTO 600
530 BOX 33+12*GX,12*GY-10,34+12*GX,12*GY-4,1,1:LINE 32+12*GX,12*GY-4,32+12*GX,12*GY-2
540 BOX 32+12*GX,12*GY-8,35+12*GX,12*GY-7,1,1:LINE 31+12*GX,12*GY-8,32+12*GX,12*GY-7
550 LINE 36+12*GX,12*GY-8,37+12*GX,12*GY-7
560 LINE 35+12*GX,12*GY-4,35+12*GX,12*GY-2
570 LINE 31+12*GX,12*GY-8,30+12*GX,12*GY-7
580 X=GX:Y=GY:RETURN
590 IF GX=CX(1) AND GY=CY(1) THEN 1130 ELSE IF GX=CX(2) AND GY=CY(2) THEN  GOTO 1130 
600 G=ASC(INKEY$)
610 IF G=98 AND ZD>0 THEN ZD=ZD-1: FOR I=1 TO 100:CIRCLE 80,40,I:NEXT I: GOTO 1070
620 IF G=20 AND MID$(GC$(GX,GY),1,1)="0" THEN GY=GY-1:680
630 IF G=21 AND MID$(GC$(GX,GY),2,1)="0" THEN GY=GY+1:680
640 IF G=22 AND MID$(GC$(GX,GY),4,1)="0" THEN GX=GX+1:680
650 IF G=23 AND MID$(GC$(GX,GY),3,1)="0" THEN GX=GX-1:680
660 IF G=13 THEN 680 
670 GOTO 590 
680 IF GX=0 OR GX=7 THEN LIFE=LIFE+5:1070
690 IF GY=0 OR GY=7 THEN LIFE=LIFE+5:1070
700 BOX 30+12*X,12*Y-10,38+12*X,12*Y-2,1,0
710 GOSUB 530
720 A=1:IF CX(1)=CX(2) AND CY(1)=CY(2) THEN D(2)=0:CX(2)=7:CY(2)=7:REM 智能
730 L=0:Q=0:O=2:I=2:ON D(A) GOTO 740,790
740 I=CX(A)-GX:O=CY(A)-GY
750 IF I=0 AND O=0 THEN 1130 ELSE IF I=0 THEN  GOTO 790
760 II=I/ABS(I)
770 IF MID$(GC$(CX(A),CY(A)),(7-II)/2,1)="0" THEN CX(A)=CX(A)-II:Q=Q+1:L=0: GOTO 840
780 L=L+1: GOTO 890
790 O=CY(A)-GY:I=CX(A)-GX:IF O=0 AND I=0 THEN 1130
800 IF O=0 THEN 740
810 OO=O/ABS(O)
820 IF MID$(GC$(CX(A),CY(A)),(3-OO)/2,1)="0" THEN CY(A)=CY(A)-OO:Q=Q+1:L=0: GOTO 840
830 L=L+1: GOTO 890
840 FOR H=1 TO 100:NEXT H:ON A GOTO 850,870
850 BOX 30+12*XX,12*YY-10,38+12*XX,12*YY-2,1,0:I=1:ON D(1)GOSUB 910,980
860 IF Q=2 THEN 900 ELSE ON D(A) GOTO 740,790
870 BOX 30+12*DX,12*DY-10,38+12*DX,12*DY-2,1,0:I=2:ON D(2)GOSUB 910,980
880 IF Q=2 THEN 900 ELSE ON D(A) GOTO 740,790
890 IF L=1 THEN ON D(A) GOTO 790,740 ELSE  GOTO 900
900 IF A=2 THEN 590 ELSE IF D(2)=0 THEN 590 ELSE A=A+1: GOTO 730
910 BOX 33+12*CX(I),12*CY(I)-10,34+12*CX(I),12*CY(I)-7,0,1
920 BOX 32+12*CX(I),12*CY(I)-9,35+12*CX(I),12*CY(I)-8,0,1
930 BOX 32+12*CX(I),12*CY(I)-6,35+12*CX(I),12*CY(I)-3,0,1
940 LINE 31+12*CX(I),12*CY(I)-7,31+12*CX(I),12*CY(I)-4
950 LINE 36+12*CX(I),12*CY(I)-7,36+12*CX(I),12*CY(I)-4
960 IF I=1 THEN XX=CX(1):YY=CY(1) ELSE DX=CX(2):DY=CY(2)
970 RETURN
980 BOX 32+12*CX(I),12*CY(I)-8,35+12*CX(I),12*CY(I)-5,1,1
990 LINE 33+12*CX(I),12*CY(I)-9,30+12*CX(I),12*CY(I)-6
1000 LINE 34+12*CX(I),12*CY(I)-9,37+12*CX(I),12*CY(I)-6
1010 LINE 31+12*CX(I),12*CY(I)-10,31+12*CX(I),12*CY(I)-9
1020 LINE 36+12*CX(I),12*CY(I)-10,36+12*CX(I),12*CY(I)-9
1030 LINE 36+12*CX(I),12*CY(I)-4,36+12*CX(I),12*CY(I)-3
1040 LINE 31+12*CX(I),12*CY(I)-4,31+12*CX(I),12*CY(I)-3
1050 IF I=1 THEN XX=CX(1):YY=CY(1) ELSE DX=CX(2):DY=CY(2)
1060 RETURN
1070 LV=LV+1:TG=INT(LV/10)
1080 IF LV=101 THEN 2360
1090 IF LV-TG*10=1 THEN 1300
1100 IF LIFE>60 THEN LIFE=60
1110 POKE 800,LV:POKE 801,LIFE:POKE 802,ZD:CLEAR
1120 LV=PEEK (800):LIFE=PEEK (801):ZD=PEEK (802): GOTO 110
1130 IF LIFE<16 THEN CLS:PRINT " "," ","     GAME OVER":A$=INKEY$:END
1140 CLS:PRINT "失败啊.","  Try again?(y/n)  ":S$=INKEY$
1150 IF S$="y" THEN LIFE=LIFE-16: GOTO 1100
1160 IF S$="n" THEN 100 ELSE 1130
1170 GRAPH:PRINT "存档选择  F1新建存档Loading..."
1180 OPEN "PYRAMID-LOGO.DAT" FOR RANDOM AS #1 LEN=20:FIELD #1,2 AS A$,2 AS B$,1 AS C$
1190 FOR I=0 TO 3:GET #1,I+801:LV(I)=VAL(A$):LIFE(I)=VAL(B$):ZD(I)=VAL(C$):NEXT 
1200 FOR I=0 TO 3:LOCATE I+2,1:PRINT "LV:";LV(I);" ";:LOCATE I+2,6:PRINT "HP:";LIFE(I);
1210 LOCATE I+2,11:PRINT "炸弹:";ZD(I);:NEXT :I=0
1220 BOX 1,I*16+16,159,I*16+31,1,2:A=ASC(INKEY$)
1230 BOX 1,I*16+16,159,I*16+31,1,2:I=I+(A=21)-(A=20)
1240 IF I<0 THEN I=3 ELSE IF I>3 THEN I=0
1250 IF A=13 THEN LV=LV(I):LIFE=LIFE(I):ZD=ZD(I): GOTO 1290
1260 IF A=28 THEN LSET A$="01":LSET B$="60":LSET C$="0":PUT #1,I+801: GOTO 1190
1270 IF A=27 THEN CLS:CLOSE #1:2560
1280 GOTO 1220
1290 ON INT(LV/10+1) GOTO 1500,1590,1640,1700,1800,1870,1940,2050,2160,2270,2360
1300 CLS:PRINT "恭喜过关 选择存档":CLOSE #1
1310 OPEN "PYRAMID-LOGO.DAT" FOR RANDOM AS #1 LEN=20:FIELD #1,2 AS A$,2 AS B$,1 AS C$
1320 FOR I=0 TO 3:GET #1,I+801:LV(I)=VAL(A$):LIFE(I)=VAL(B$):ZD(I)=VAL(C$):NEXT 
1330 FOR I=0 TO 3:LOCATE I+2,1:PRINT "LV:";LV(I);:LOCATE I+2,6:PRINT "HP:";LIFE(I);
1340 LOCATE I+2,11:PRINT "炸弹:";ZD(I);:NEXT :I=0
1350 BOX 1,I*16+16,159,I*16+31,1,2:A=ASC(INKEY$)
1360 BOX 1,I*16+16,159,I*16+31,1,2:I=I+(A=21)-(A=20)
1370 IF I<0 THEN I=3 ELSE IF I>3 THEN I=0
1380 IF A=13 THEN 1400
1390 GOTO 1350
1400 LSET A$=STR$(LV):LSET B$=STR$(LIFE):LSET C$=STR$(ZD)
1410 IF LIFE<10 THEN LSET B$="0"+B$
1420 IF LIFE>60 THEN LSET B$="60"
1430 PUT #1,I+801:ON TG GOTO 1590,1640,1700,1800,1870,1940,2050,2160,2270,2360
1440 Y=LV-INT(LV/10)*10:IF Y=0 THEN Y=10
1450 BOX 0,57,8,60,(Y>0):BOX 9,57,17,60,(Y>1):BOX 18,57,26,60,(Y>2):BOX 27,57,35,60,(Y>3)
1460 BOX 4,53,12,56,(Y>4):BOX 13,53,21,56,(Y>5):BOX 22,53,30,56,(Y>6)
1470 BOX 8,49,16,52,(Y>7):BOX 17,49,25,52,(Y>8):BOX 13,45,21,48,(Y>9):RETURN
1480 Y=LV-INT(LV/10)*10:ON TG GOTO 1490,1590,1640,1700,1800,1870,1940,2050,2160,2270
1490 REM 剧本
1500 REM 1
1510 CLS:LOCATE 3,2:PRINT " 第1章 时空之混乱":Q=1: GOTO 2540
1520 CLS:PRINT ":终于得到了6块三角石板,可以通关了.",:A$=INKEY$
1530 PRINT "那一战天昏地暗(回家收衣服),你艰难胜出.":A$=INKEY$
1540 CLS:PRINT "神秘人:你是NO.50002个通关的.只好永远留在这里.":A$=INKEY$
1550 CLS:PRINT "你推开神秘人.冲进时空转换器.身后传来一声叹息.":A$=INKEY$:Q=1: GOTO 2520
1560 CLS:PRINT ":怎么来到了埃及?只有这金字塔?",:A$=INKEY$
1570 CLS:PRINT "黑人类：你好,我是NO.50001.我出来了.送你1个炸弹":ZD=ZD+1:A$=INKEY$
1580 CLS:PRINT ":-_-b":A$=INKEY$:Q=2: GOTO 2520
1590 CLS:LOCATE 3,6:PRINT " 第2章 迷失":Q=2: GOTO 2540
1600 CLS:PRINT ":那不是顾炎武吗?怎么他也在这?":A$=INKEY$
1610 PRINT "顾炎武:你好,我是..":A$=INKEY$
1620 CLS:PRINT "天空中传来一阵鸟鸣.":A$=INKEY$
1630 CLS:PRINT "顾炎武:BYE-BYE!":A$=INKEY$:PRINT "我汗..":A$=INKEY$:Q=2: GOTO 2520 
1640 CLS:LOCATE 3,2:PRINT " 第3章 新的方向":Q=3: GOTO 2540
1650 CLS:PRINT "谷虫:想不到还有和黑人类一样聪明的人.":A$=INKEY$
1660 CLS:PRINT ":你是谁?":A$=INKEY$
1670 CLS:PRINT "谷虫:我是谁?好久没想过了.你还会见到我的.":A$=INKEY$
1680 CLS:PRINT "说完.谷虫就摆出很酷的造型,消失了.":A$=INKEY$
1690 CLS:PRINT ":我会加油的.":A$=INKEY$:Q=2: GOTO 2520
1700 CLS:LOCATE 3,6:PRINT " 第4章 抉择":Q=4: GOTO 2540
1710 CLS:PRINT "你来到一个机器面前",":这是什么?":A$=INKEY$
1720 CLS:PRINT "机器:请回答我的问题,有奖哦.":A$=INKEY$
1730 CLS:PRINT "1.wqxfans的成立日期?";"A.04.10.1 B.04.8.8","C.05.1.1 D.05.2.9":A$=INKEY$
1740 IF A$="b" THEN ZD=ZD+1 ELSE IF A$="a" OR A$="c" OR A$="d" THEN 1750 ELSE 1730
1750 CLS:PRINT "2.EMAX的站长是谁?","A.athlon  B.老F","C.LENDY  D.SUN":A$=INKEY$
1760 IF A$="c" THEN ZD=ZD+1 ELSE IF A$="a" OR A$="b" OR A$="d" THEN 1770 ELSE 1750
1770 CLS:PRINT "3.你是第几个通关的?","A.500001  B.50001","C.500000 D.50002":A$=INKEY$
1780 IF A$="d" THEN ZD=ZD+1 ELSE IF A$="a" OR A$="b" OR A$="c" THEN 1790 ELSE 1770
1790 CLS:PRINT ":谢谢了.":A$=INKEY$:Q=2: GOTO 2520
1800 CLS:LOCATE 3,2:PRINT " 第5章 神秘智者":Q=5: GOTO 2540
1810 CLS:PRINT "智者:你好,我无所不知.哈哈.":A$=INKEY$
1820 CLS:PRINT ":那你知道浪人乙的秘密吗?":A$=INKEY$
1830 CLS:PRINT "智者:其实偶就是他啦.偶是GGV的托啊.":A$=INKEY$
1840 CLS:PRINT ":怪不得我怎么也没得到你的东西.":A$=INKEY$
1850 CLS:PRINT "智者:加油.这里可好玩了.":A$=INKEY$
1860 CLS:PRINT ":谢谢.(只见智者化为大鸟飞去.)":A$=INKEY$:Q=2: GOTO 2520
1870 CLS:LOCATE 3,3:PRINT "第6章 又见黑人类":Q=6: GOTO 2540
1880 CLS:PRINT "黑人类:又遇见你了.偶又通关了.NO.50003":A$=INKEY$
1890 CLS:PRINT ":-_-b...........强男啊！！！":A$=INKEY$
1900 CLS:PRINT "黑人类:偶知道YOBDC.(大家不要学他)":A$=INKEY$
1910 CLS:PRINT ":-_-b...........没品啊！！！":A$=INKEY$
1920 CLS:PRINT "黑人类:我去了.这的密码是......(谷虫:少儿不宜):A$= INKEY$ 
1930 CLS:PRINT ":我还是自己努力吧.":A$=INKEY$:Q=2: GOTO 2520
1940 CLS:LOCATE 3,4:PRINT "第7章 广告时间":Q=7: GOTO 2540
1950 CLS:PRINT ":这里也有广告啊！我汗..":A$=INKEY$
1960 CLS:PRINT "GameboyLV:欢迎到wqxfans.":A$=INKEY$
1970 CLS:PRINT "LENDY:欢迎到EMAX.":A$=INKEY$
1980 CLS:PRINT "LEE:欢迎购买LAVA2":A$=INKEY$
1990 CLS:PRINT "RWK:大家不急.仙境很快搞定.":A$=INKEY$
2000 CLS:PRINT "athlon&老F:神州2值得期待.":A$=INKEY$
2010 CLS:PRINT "谷虫:mathtool世界第一.":A$=INKEY$
2020 CLS:PRINT "黑人类:这是我的台词.":A$=INKEY$
2030 CLS:PRINT "辉:翠花,上NC3000啊!":A$=INKEY$
2040 CLS:PRINT ":我还是继续吧.":A$=INKEY$:Q=2: GOTO 2520
2050 CLS:LOCATE 3,4:PRINT "第8章 金字塔":Q=8: GOTO 2540
2060 CLS:PRINT "(你好)":A$=INKEY$
2070 CLS:PRINT ":什么人?是人还是鬼?":A$=INKEY$
2080 CLS:PRINT ":我是这里的灯泡.":A$=INKEY$
2090 CLS:PRINT ":这里到底有多深啊.我走的好郁闷啊.":A$=INKEY$
2100 CLS:PRINT ":你快胜利了.慢慢走,不要急.":A$=INKEY$
2110 CLS:PRINT ":对了.这里的密码是什么?":A$=INKEY$
2120 CLS:PRINT ":你进DEBUG,按F2.(谷虫:不要乱试!!!)":A$=INKEY$
2130 CLS:PRINT ":你想害我啊.":A$=INKEY$
2140 CLS:PRINT ":开玩笑的...":A$=INKEY$
2150 CLS:PRINT ":谁把灯关了.":A$=INKEY$:Q=2: GOTO 2520
2160 CLS:LOCATE 3,4:PRINT "第9章 路遇强盗":Q=9: GOTO 2540
2170 CLS:PRINT ":这里怎么躺了个..":A$=INKEY$
2180 CLS:PRINT "辉:我就是有梅花般刚强.冰川般冷酷.令人哲服的内函.酷的不行的.."
2190 BOX 88,0,104,15,1,2:BOX 40,16,56,31,1,2:BOX 32,32,48,47,1,2:BOX 72,32,88,47,1,2
2200 A$=INKEY$:CLS:PRINT ":难到你就是那个传说中的强盗头头--梅川内酷?":A$=INKEY$
2210 CLS:PRINT "辉:YES!道上的人都叫我一声辉郎.":A$=INKEY$
2220 CLS:PRINT ":(今天完了)你的那些兄弟呢?":A$=INKEY$
2230 CLS:PRINT "辉:诶~人人都去支持三个代表了.我也去咯.下雨了?回家收衣服..":A$=INKEY$
2240 CLS:PRINT ":(还好命大)BYE-BYE.":A$=INKEY$
2250 CLS:PRINT "辉:等一下,给你一个炸弹自杀使用.":A$=INKEY$:ZD=ZD+1
2260 CLS:PRINT ":-_-好...好...":A$=INKEY$:Q=2: GOTO 2520
2270 CLS:LOCATE 3,4:PRINT "第10章 大战之前":Q=10: GOTO 2540
2280 CLS:PRINT "谷虫:恭喜你.还有最后的一关了.你的好朋友想对你说两句":A$=INKEY$
2290 CLS:PRINT "清虚:不愧是我的好徒弟.加油.为师再教你一招四环套月.":A$=INKEY$
2300 CLS:PRINT "捕快:谢谢你为我解决了不少恶人.":A$=INKEY$
2310 CLS:PRINT "恶人:谁在叫我?我也感谢你让我练了不少级.":A$=INKEY$
2320 CLS:PRINT "中年妇人:谢谢你帮我找....我要说什么的?":A$=INKEY$
2330 CLS:PRINT "顾炎武:我的确收你的学费太多了.潜能给的太少.送你个炸弹当补偿.":A$=INKEY$
2340 CLS:PRINT ":谢谢.(怎么学艺术人生?)大家都这么关心我.(说罢老泪纵横)":A$=INKEY$
2350 CLS:PRINT "辉:你要加油(一边把你推了下去)":ZD=ZD+1:A$=INKEY$:Q=2: GOTO 2520
2360 CLS:LOCATE 3,4:PRINT "第11章 ?????":Q=11: GOTO 2540
2370 CLS:PRINT ":哈哈.偶终于通关了....":A$=INKEY$
2380 CLS:PRINT "谷虫:哈哈.你还不赖吗.虽然你和黑人类还差的..":A$=INKEY$
2390 CLS:PRINT ":-_-b.他也太#@&*^~":A$=INKEY$
2400 CLS:PRINT "黑人类:我又通关了.哈哈.哦,你终于过了.":A$=INKEY$
2410 CLS:PRINT ":-_-b.我怎么才能回家啊!":A$=INKEY$
2420 CLS:PRINT "谷虫:让我来想一下..........":A$=INKEY$
2430 CLS:PRINT ":这你还要想？":A$=INKEY$
2440 CLS:PRINT "谷虫:方法就是..你关掉机器..好好学习":A$=INKEY$
2450 CLS:PRINT ":I will give you some color see see.":A$=INKEY$
2460 CLS:PRINT "谷虫:看你的英语.果然要好好学.":A$=INKEY$
2470 CLS:PRINT ":算了.我要向黑人类学习":A$=INKEY$
2480 CLS:PRINT "谷虫:-_-b.一个极端到另一个极端.":A$=INKEY$
2490 CLS:PRINT "你正要重新玩的时候.一个惊雷打了下来.你回到了现实.":A$=INKEY$
2500 CLS:PRINT "你在干什么?把WQX拿来...(老师啊...黑啊..)":A$=INKEY$
2510 CLS:PRINT " "," ","       THE END":A$=INKEY$:A=1:B=3: GOTO 2680
2520 FOR I=0 TO 40 STEP 0.5:BOX I*2,I,160-I*2,80-I:NEXT I
2530 ON Q GOTO 1560,1100
2540 FOR I=1 TO 160 :LINE I,32,I,48,2:NEXT I:A$=INKEY$
2550 ON Q GOTO 1520,1600,1650,1710,1810,1880,1950,2060,2170,2280,2370
2560 LOCATE 1,3:PRINT "金字塔大冒险","1.开始冒险","2.开发团体","3.退出游戏","LGSOFT出品";
2570 AX=120:AY=65:A=35:B=10:PI=3.14159/18
2580 FOR I=1 TO 36:A(I)=AX+A*SIN(I*PI):B(I)=AY-B*COS(I*PI):NEXT 
2590 FOR Q=0 TO 999:I=Q-INT(Q/9)*9+1:LINE 120,16,A(I),B(I):LINE A(I),B(I),A(I+9),B(I+9)
2600 LINE 120,16,A(I+9),B(I+9):LINE A(I+9),B(I+9),A(I+18),B(I+18)
2610 LINE 120,16,A(I+18),B(I+18):LINE A(I+18),B(I+18),A(I+27),B(I+27)
2620 LINE 120,16,A(I+27),B(I+27):LINE A(I),B(I),A(I+27),B(I+27):IF PEEK (199)>128 THEN 2650
2630 FOR K=1 TO 800:NEXT K:BOX 81,16,159,80,1,0
2640 NEXT Q: GOTO 2590
2650 H$=INKEY$:IF H$="b" THEN  GOTO 1170
2660 IF H$="m" THEN A=3:B=5: GOTO 2680:END
2670 GOTO 2730
2680 POKE 801,A:POKE 802,B:CLEAR:A=PEEK (801):B=PEEK (802)
2690 OPEN "PYRAMID-LOGO.DAT" FOR RANDOM AS #1 LEN=20:GRAPH
2700 FIELD #1,20 AS A$: FOR I=704 TO 803:POKE I,0:NEXT : FOR K=A*80+1 TO B*80-1
2710 fseek 1,k*20-20:fread 1,24556,20:CALL 20560: FOR I=1 TO 250:NEXT I:CALL 20480
2720 NEXT K:GET #1,B*80:CALL 20560:A$=INKEY$:ON B/2-.5 GOTO 100,2750
2730 IF H$="n" THEN A=1:B=3: GOTO 2680
2740 GOTO 2590
2750 END