3 N$(2)="曹操":N$(3)="董卓":N$(4)="袁绍":N$(5)="袁术":N$(6)="孙策"
4 N$(7)="刘备":N$(8)="刘表":N$(9)="刘璋":N$(10)="马腾":N$(1)="天地会"
10 I=1:Y=1:GRAPH
15 LOCATE 3,1:PRINT "制作人:季竹西  贾轩":A$=INKEY$
20 CLS:LOCATE 3,7:PRINT "逐鹿中原":A$=INKEY$
30 CLS:PRINT "190年1月,天地会成立,从此中国进入新的时代.":A$=INKEY$
40 CLS:PRINT "但是由于客观条件,革命注定不能一帆风顺.":A$=INKEY$
50 CLS:PRINT "你必须顽强抵抗住各派军阀的疯狂围剿.":A$=INKEY$
60 CLS:PRINT "你的最终目标是:统一中国.":A$=INKEY$
65 CLS:LOCATE 3,7:PRINT "祝你好运!":A$=INKEY$
70 A(1)=25000:A(2)=30000:A(3)=40000:A(4)=35000:A(5)=30000:A(6)=25000
75 A(7)=15000:A(8)=25000:A(9)=20000:A(10)=15000
80 B(1)=85:B(2)=75:B(3)=60:B(4)=65:B(5)=55:B(6)=70
85 B(7)=70:B(8)=50:B(9)=50:B(10)=65
90 C(1)=20000:C(2)=30000:C(3)=40000:C(4)=35000:C(5)=30000:C(6)=25000
91 C(7)=15000:C(8)=25000:C(9)=20000:C(10)=15000
95 D(1)=60:D(2)=120:D(3)=160:D(4)=120:D(5)=80:D(6)=120
96 D(7)=60:D(8)=300:D(9)=320:D(10)=260
100 TIM=1:CLS:PRINT "1战争2征兵3训练4挣钱"
105 PRINT "5查询6退出";" 公元";F+190;"年"
110 PRINT "兵力";A(1);"训练";B(1)
120 PRINT "金钱";C(1);"领土";D(1)
130 A$=INKEY$
140 IF A$="j" THEN END
150 IF A$="h" THEN GOTO 300
160 IF A$="g" THEN GOTO 500
170 IF A$="m" THEN GOTO 600
180 IF A$="n" THEN GOTO 700
190 IF A$="b" THEN GOTO 200
195 GOTO 100
200 CLS:PRINT "进攻谁?(1-9进攻,0取消)";:INPUT A
210 IF A<0 OR A>9 OR A<>INT(A) THEN PRINT "无效数据!":A$=INKEY$:GOTO 200
215 IF A=0 THEN 100
220 IF E(A+1)=1 THEN PRINT "此党已经解散!":A$=INKEY$:GOTO 200
230 V(1)=1:V(2)=A+1:GOTO 4921
300 FOR H=2  TO 10 :IF E(H)=0 THEN CLS:PRINT "==";H-1;N$(H);"==": ELSE GOTO 340
310 PRINT "兵力";A(H);"训练";B(H)
320 PRINT "金钱";C(H);"领土";D(H)
330 A$=INKEY$
340 NEXT H
350 GOTO 100
500 M=INT(RND(1)*D(1)^0.8*60+D(1)^0.8*20)
510 CLS:PRINT N$(1);"派军队四处打劫,缴获";M;"元军费.":A$=INKEY$
520 C(1)=C(1)+M:GOTO 6009
600 S=INT(A(1)^0.8/10):IF C(1)<S THEN PRINT "军费不够!":A$=INKEY$:GOTO 100
610 B(1)=B(1)+5:C(1)=C(1)-S:CLS:PRINT N$(1);"军战斗力增强!":A$=INKEY$
620 IF B(1)>100 THEN B(1)=100
630 GOTO 6009
700 M=INT(RND(1)*D(1)^0.8*30+D(1)^0.8*10)
710 IF C(1)<M THEN PRINT "军费不够!":A$=INKEY$:GOTO 100
720 C(1)=C(1)-M:A(1)=A(1)+M:IF B(1)>30 THEN B(1)=B(1)-1
730 CLS:PRINT N$(1);"四处征兵,兵力增加";M;"人.":A$=INKEY$:GOTO 6009
1000 I=I+1
1010 IF E(I)=0 THEN K=INT(RND(1)*4+1): ELSE GOTO 6009
1020 IF K=2 THEN GOTO 2000
1030 IF K=3 THEN GOTO 3000
1040 IF K=4 THEN GOTO 4000
1050 IF K=1 THEN GOTO 4900
1500 GOTO 1000
2000 M=INT(RND(1)*D(I)^0.8*30+D(I)^0.8*10)
2010 IF C(I)<M THEN GOTO 1010
2020 C(I)=C(I)-M:A(I)=A(I)+M
2030 CLS:PRINT N$(I);"四处征兵,兵力增加";M;"人.":A$=INKEY$:IF B(I)>30 THEN B(I)=B(I)-1
2040 GOTO 6009
3000 S=INT(A(I)^0.8/10):IF C(I)<S THEN GOTO 1010
3010 B(I)=B(I)+2:C(I)=C(I)-S:CLS:PRINT N$(I);"军战斗力增强!":A$=INKEY$
3020 IF B(I)>100 THEN B(I)=100
3030 GOTO 6009
4000 M=INT(RND(1)*D(I)^0.8*60+D(I)^0.8*20)
4010 CLS:PRINT N$(I);"派军队四处打劫,缴获";M;"元军费.":A$=INKEY$
4020 C(I)=C(I)+M:GOTO 6009
4900 T=INT(RND(1)*10+1):IF T=I OR E(T)=1 THEN GOTO 4900
4910 V(1)=I:V(2)=T:IF A(V(1))<A(V(2))/2 THEN GOTO 1010
4921 CLS:PRINT N$(V(1));"进攻";N$(V(2));"!":A$=INKEY$
5000 W=A(V(1)):WW=A(V(2)):R=1:ROU=10*TIM
5005 CLS:PRINT "第";R;"回合"
5010 PRINT N$(V(1));A(V(1));"人"
5020 PRINT N$(V(2));A(V(2));"人"
5040 O=A(V(1)):P=A(V(2))
5050 OO=INT((RND(1)*O^0.8/2.5*0.4+O^0.8/2.5*0.8)*B(V(1))^2/10000)
5060 IF P-OO<=0 THEN A(V(2))=0 :PRINT N$(V(2));"军全军覆没!":E(V(2))=1:F(Y)=V(2):A$=INKEY$
5070 IF A(V(2))=0 THEN D(V(1))=D(V(1))+D(V(2)):Y=Y+1:GOTO 9000
5075 A(V(2))=A(V(2))-OO
5080 OO=INT((RND(1)*P^0.8/2.5*0.4+P^0.8/2.5*0.8)*B(V(2))^2/10000)
5090 IF O-OO<=0 THEN A(V(1))=0:PRINT N$(V(1));"军全军覆没!":E(V(1))=1:F(Y)=V(1):A$=INKEY$
5100 IF A(V(1))=0 THEN D(V(2))=D(V(2))+D(V(1)):Y=Y+1:GOTO 9000
5105 A(V(1))=A(V(1))-OO
5110 R=R+1 :IF R>ROU THEN GOTO 6000
5120 GOTO 5005
6000 E=W-A(V(1)):EE=WW-A(V(2))
6001 IF E<EE THEN CLS:PRINT N$(V(1));"胜利!";
6002 IF E<EE THEN J=INT(RND(1)*D(V(2))/10*0.4+D(V(2))/10*0.8)*3:PRINT N$(V(1));"得到土地";J
6003 IF E<EE THEN D(V(1))=D(V(1))+J:D(V(2))=D(V(2))-J:GOTO 6008
6004 CLS:PRINT N$(V(1));"败北!";
6005 J=INT(RND(1)*D(V(1))/10*0.4+D(V(1))/10*0.8):PRINT N$(V(1));"失去土地";J
6006 D(V(2))=D(V(2))+J:D(V(1))=D(V(1))-J:GOTO 6008
6008 A$=INKEY$
6009 IF TIM<>1 THEN TIM=1:GOTO 8000
6010 IF I<10 THEN GOTO 1500: ELSE I=1:F=F+1
6100 IF F<>5 THEN 6150
6105 IF A(3)<=0 THEN 6130
6106 CLS:PRINT "公元195年,董卓被义子吕布所杀.":A$=INKEY$
6110 CLS:PRINT "吕布掌握大权,士兵不满他的统治,逃跑大半.":A$=INKEY$
6120 N$(3)="吕布":A(3)=INT(A(3)*(0.6*RND(1)+0.2))
6130 IF A(6)<=0 THEN 6150
6135 CLS:PRINT "公元195年,孙策被奸贼所杀,其弟孙权甫一即位,就重视军事,兵力大增.":A$=INKEY$
6140 N$(6)="孙权":A(6)=INT(A(6)*(0.2*RND(1)+1.1)):GOTO 8000
6150 IF F<>10 THEN 6180
6155 IF A(2)<=0 OR A(4)<=0 THEN 6180
6160 CLS:PRINT "公元200年,袁绍与曹操为争夺霸主地位,征兵无数,发动官渡之战.":A$=INKEY$
6170 V(1)=2:V(2)=4:TIM=2.5:A(2)=A(2)+15000:A(4)=A(4)+15000:GOTO 4921
6180 IF F<>15 THEN 6210 
6185 IF A(4)<=0 THEN 6210
6190 CLS:PRINT "公元205年,袁绍病逝,袁谭即位,其手下纷纷散去.":A$=INKEY$
6200 N$(4)="袁谭":A(4)=INT(A(4)*(0.2*RND(1)+0.4)):GOTO 8000 
6210 IF F<>17 THEN 6240
6215 IF A(7)<=0 THEN 6240
6220 CLS:PRINT "公元207年,刘备在诸葛亮的辅佐下,实力突飞猛进.":A$=INKEY$
6230 A(7)=INT(A(7)*(0.4*RND(1)+1.8)):GOTO 8000
6240 IF F<>18 THEN 6280 
6245 IF A(6)<=0 OR A(2)<=0 THEN 6280 
6250 CLS:PRINT "公元208年,曹操大肆招兵买马,南征东吴.":A$=INKEY$
6260 CLS:PRINT "孙权集合国内所有可用之兵,发动赤壁之战.":A$=INKEY$
6270 V(1)=2:V(2)=6:TIM=2.5:A(2)=A(2)+15000:A(6)=A(6)+20000:GOTO 4921
6280 IF F<>20 THEN 6320 
6285 IF A(10)<=0 OR A(2)<=0 THEN 6320 
6290 CLS:PRINT "公元210年,马腾被曹操所害,马超继承父业.":A$=INKEY$
6300 CLS:PRINT "但是在曹操阴谋的影响下,手下四分五裂.":A$=INKEY$
6310 N$(10)="马超":A(10)=INT(A(10)*(0.4*RND(1)+0.4)):GOTO 8000 
6320 IF F<>30 THEN 6350 
6325 IF A(2)<=0 THEN 6350 
6330 CLS:PRINT "公元220年,曹操病逝,曹丕即位.":A$=INKEY$
6340 N$(2)="曹丕":GOTO 8000 
6350 IF F<>33 THEN 6380 
6355 IF A(7)<=0 THEN 6380 
6360 CLS:PRINT "公元223年,刘备病逝,刘禅即位,他终日享乐,不问军事,士兵疏于训练.":A$=INKEY$
6370 N$(7)="刘禅":B(7)=B(7)-10:GOTO 8000 
6380 IF F<>45 THEN 6410 
6385 IF A(2)<=0 THEN 6410 
6390 CLS:PRINT "公元235年,司马氏篡位后,穷兵黩武,罗致天下豪杰.":A$=INKEY$
6400 N$(2)="司马氏":GOTO 8000 
6410 IF F<>17 THEN 6440 
6415 IF A(2)<=0 OR A(8)<=0 THEN 6440 
6420 CLS:PRINT "公元207年,曹操大兵压境,刘氏兵马国土,尽属曹家.":A$=INKEY$
6430 E(8)=1:BTD=D(8):A(2)=A(2):C(2)=C(2):D(2)=D(2):Y=Y+1:F(Y)=8:GOTO 8000 
6440 IF F<>19 THEN 6470 
6445 IF A(2)<=0 OR A(7)<=0 THEN 6470 
6450 CLS:PRINT "公元209年,刘备将以前刘表的土地据为己有.":A$=INKEY$
6460 D(2)=D(2)-BTD:D(7)=D(7)+BTD:GOTO 8000 
6470 IF F<>23 THEN 6500 
6475 IF A(9)<=0 OR A(7)<=0 THEN 6500 
6480 CLS:PRINT "公元213年,刘备入蜀,夺了刘璋的兵马土地.":A$=INKEY$
6490 E(9)=1:A(7)=A(7)+A(9):C(7)=C(7)+C(9):D(7)=D(7)+D(9):Y=Y+1:F(Y)=9:GOTO 8000 
6500 GOTO 8000 
8000 IF Q=1 THEN GOTO 1000: ELSE GOTO 100 
9000 IF E(1)=1 THEN Q=1 
9005 FOR G=1  TO 10 
9010 IF E(G)=1 THEN GG=GG+1 
9020 NEXT G
9030 IF GG<9 THEN GG=0:GOTO 6009 
9040 FOR M=1  TO 10
9050 IF E(M)=0 THEN F(10)=M
9060 NEXT M
9070 CLS:PRINT "公元";F+190;"年,";N$(F(10));"一统天下!":A$=INKEY$:CLS
9080 FOR N=10  TO 1 STEP -1
9090 PRINT "第";11-N;"名:";N$(F(N)):A$=INKEY$
9100 NEXT N